/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

import queries_rdb;
import builder_errors;
import queries_errors;

modeltype RDB uses 'http://www.collectionspro.eu/jam/mm/rdb';
modeltype ERR uses 'http://www.collectionspro.eu/jam/mm/errors';

transformation validator_postgresql(in inModel : RDB, in inErrorModel : ERR, out outErrorModel : ERR);

main() {	
	log("Postgresql validation of model started...");
	inErrorModel.rootObjects()[ErrorLog].deepclone();
	if(inModel.rootObjects()->exists(root | root.oclIsTypeOf(RDB::Structure)))then{
		var errorLog : ErrorLog := outErrorModel.rootObjects()![ErrorLog];
		inModel.rootObjects()![Structure].isModelValid(errorLog);
		errorLog.print();
		log("");
	}else{
		log("modelRoots doesn't conform to RDB::Structure type");
	}endif;
}
query getValidatorPostgresqlId() : String{
	return "validator_postgresql.qvto";
}

query rdb::Structure::isModelValid(inout errorLog : ErrorLog ):Boolean{
	var tableUnique : Boolean := true;
	var schemaUnique : Boolean := true;
	var columnUnique : Boolean := true;
	var constraintUnique : Boolean := true;
	var pkCount : Boolean := true;
	
	var validIdentifiers : Boolean := true;
	
	var schemas : Set(Schema) := self.schemas;
	var tables : Bag(Table) := schemas->tables;
	//kontrola identifikatoru schematu
	schemas->forEach(schema){
		if(not schema.hasUniqueName())then{
			var errorMessage : String := "Schema \"" + schema.name + "\" has duplicit name as another schema in "
				+"generation";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			schemaUnique := false;
		}endif;	
		if(not isValidIdentifier(schema.name))then{
			var errorMessage : String := "Invalid schema identifier \"" + schema.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			validIdentifiers:= false;
		}endif;
	};
	
	//kontrola identifikatoru tabulky
	tables->forEach(table){
		if(not table.hasUniqueName())then{
			var errorMessage : String := "Schema \"" + table.owningSchema.name + "\" contains duplicit table name - \"" 
				+ table.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			tableUnique := false;
		}endif;
		if(not isValidIdentifier(table.name))then{
			var errorMessage : String := "Invalid table identifier \"" + table.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			validIdentifiers:= false;			
		}endif;
	};
	//kontrola identifikatoru sloupce
	tables->columns->forEach(column){
		if(not column.hasUniqueName())then{
			var errorMessage : String := "Schema \"" + column.owningTable.owningSchema.name + "\" contains duplicit " 
				+ "column name \"" + column.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			columnUnique := false;
		}endif;
		if(not isValidIdentifier(column.name))then{
			var errorMessage : String := "Invalid column identifier \"" + column.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			validIdentifiers:= false;
		}endif;
	};
	
	tables->constraints->forEach(constraint){
		if(not constraint.hasUniqueName())then{
			var errorMessage : String := "Schema \"" + constraint.owningTable.owningSchema.name + 
					"\" contains constraint with duplicit name \"" + constraint.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			constraintUnique := false;
		}endif;
		if(not isValidIdentifier(constraint.name))then{
			var errorMessage : String := "Invalid table constraint identifier \"" + constraint.name + "\"";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			validIdentifiers := false;
		}endif;
	};
	
	tables->forEach(tbl){
		if(tbl.hasRedundantId())then{
			var errorMessage : String := "Table \"" + tbl.name + "\" in schema \"" + tbl.owningSchema.name 
					+ "\" has redundant PK";
			addValidationError(errorLog, errorMessage, getValidatorPostgresqlId());
			pkCount := false;
		}endif;
	};
	
	var valid : Boolean := tableUnique and columnUnique and schemaUnique and constraintUnique and pkCount 
			and validIdentifiers;
	return valid;
}