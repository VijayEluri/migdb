/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */
 
modeltype APP uses app('http://www.collectionspro.eu/jam/mm/app');
modeltype RDB uses rdb('http://www.collectionspro.eu/jam/mm/rdb');

transformation app_to_rdb(in inApp : APP, inout inRdb : RDB);

main() {
	assert(inApp.rootObjects()->size() = 1);
	assert(inApp.rootObjects()->forAll(oclIsKindOf(APP::ModelRoot)));
	
	log("transformation started...");
	
	var appRoot : APP::ModelRoot = inApp.rootObjects()![APP::ModelRoot];
	var appGen : APP::ModelGeneration = appRoot.modelGenerations->first();
	var rdbRoot : RDB::ModelRoot = inRdb.rootObjects()![RDB::ModelRoot];
	
	appRoot.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(appGen, rdbRoot)) then {
			log("	operation is valid " + op.repr());
			op.xmap toRdb(appGen, rdbRoot);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
}

// a mapping of a given operation
abstract mapping APP::operations::ModelOperation::
	toRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot);
	
// is a mapping valid
abstract query APP::operations::ModelOperation::
	isValid(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot) : Boolean;


/* ++++++++++ OPERATIONS ++++++++++++++++++++ */

query APP::operations::AddClass::isValid(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot): Boolean {
	return self.isAbstract = false;
}

mapping APP::operations::AddClass::toRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot) {
	// addition of a table to the schema
	rdbRoot.operations += object RDB::operations::AddTable {
		owningSchemaName := "public";
		name := self.name.toLower();
	};
	
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.name.toLower();
		name := "id";
		type := RDB::PrimitiveType::int;
		// defaultValue := "0";
	};
	
	// addition of an index to the schema
	rdbRoot.operations += object RDB::operations::AddIndex {
		owningSchemaName := "public";
		owningTableName := self.name.toLower();
		name := "IX_" + self.name.toLower() + "_id";
		columnsNames := OrderedSet {"id"};
	};
	
	// addition of a primary key to the table
	rdbRoot.operations += object RDB::operations::AddPrimaryKey {
		owningSchemaName := "public";
		owningTableName := self.name.toLower();
		name := "PK_" + self.name.toLower();
		underlyingIndexName := "IX_" + self.name.toLower() + "_id";
	};
}

query APP::operations::AddProperty::isValid(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot): Boolean {
	return true;
}

mapping APP::operations::AddProperty::toRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot) {	
	self.xmap primitivePropertyToRdb(appGen, rdbRoot);
	self.xmap embeddedPropertyToRdb(appGen, rdbRoot);
	self.xmap complexPropertyToRdb(appGen, rdbRoot);
	
	self.xmap notNullToRdb(appGen, rdbRoot);
	
	self.xmap collectionToRdb(appGen, rdbRoot);
}

mapping APP::operations::AddProperty::primitivePropertyToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { appGen.findEntity(self.type).oclIsTypeOf(APP::PrimitiveClass); }
{
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		name := self.name.toLower();
		type := appGen.findPrimitiveTypeInRdb(self.type);
		defaultValue := self.defaultValue;
	};
}

mapping APP::operations::AddProperty::embeddedPropertyToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { appGen.findEntity(self.type).oclIsTypeOf(APP::EmbeddedClass); }
{
	appGen.findEntity(self.type).oclAsType(APP::EmbeddedClass).properties->forEach(p) {
		// addition of a column to the table
		rdbRoot.operations += object RDB::operations::AddColumn {
			owningSchemaName := "public";
			owningTableName := p.owningClass.name.toLower();
			name := p.name.toLower();
			type := appGen.findPrimitiveTypeInRdb(p.type.name);
			defaultValue := self.defaultValue;
		};
	
	};
}

mapping APP::operations::AddProperty::complexPropertyToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { appGen.findEntity(self.type).oclIsTypeOf(APP::Class) and self.upperBound <> -1; }
{
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		name := self.type.toLower() + "_id";
		type := RDB::PrimitiveType::int;
		defaultValue := self.defaultValue;
	};
	
	// addition of a foreign key to the table
	rdbRoot.operations += object RDB::operations::AddForeignKey {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		name := "FK_" + self.owningClassName.toLower() + "_" + self.type.toLower();
		targetTableName := self.type.toLower();
		constrainedColumnName := self.type.toLower() + "_id";
	};
}

mapping APP::operations::AddProperty::notNullToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { self.lowerBound = 1 }
{
	// addition of a notNull constraint to the column
	rdbRoot.operations += object RDB::operations::AddNotNullConstraint {
		owningSchemaName := "public";
		owningTableName := self.owningClassName;
		owningColumnName := self.name;
		name := "NN_" + self.name.toLower();
	};
}

mapping APP::operations::AddProperty::collectionToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { self.upperBound = -1 }
{
	self.xmap generalCollectionToRdb(appGen, rdbRoot);
	self.xmap orderedCollectionToRdb(appGen, rdbRoot);
}

// 0..1 * {unique?}
mapping APP::operations::AddProperty::generalCollectionToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { true; }
{
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.type.toLower();
		name := self.owningClassName.toLower() + "_id";
		type := RDB::PrimitiveType::int;
	};
	
	// addition of a foreign key to the table
	rdbRoot.operations += object RDB::operations::AddForeignKey {
		owningSchemaName := "public";
		owningTableName := self.type.toLower();
		name := "FK_" + self.type.toLower() + "_" + self.owningClassName.toLower();
		targetTableName := self.owningClassName.toLower();
		constrainedColumnName := self.owningClassName.toLower() + "_id";
	};
}

// 0..1 * {ordered, unique?}
mapping APP::operations::AddProperty::orderedCollectionToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { self.isOrdered }
{
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.type.toLower();
		name := "ord_" + self.owningClassName.toLower();
		type := RDB::PrimitiveType::int;
	};
	
	// addition of an index to the schema
	rdbRoot.operations += object RDB::operations::AddIndex {
		owningSchemaName := "public";
		owningTableName := self.type.toLower();
		name := "UX_" + self.type.toLower() + "_ord_" + self.owningClassName.toLower();
		columnsNames := OrderedSet {"ord_" + self.owningClassName.toLower()};
	};
	
	// addition of an unique constraint to the table
	rdbRoot.operations += object RDB::operations::AddUniqueIndex {
		owningSchemaName := "public";
		owningTableName := self.type.toLower();
		name := "UK_" + self.type.toLower() + "_ord_" + self.owningClassName.toLower();
		underlyingIndexName := "UX_" + self.type.toLower() + "_ord_" + self.owningClassName.toLower();
	};
}

// unique collection


mapping APP::operations::SetParent::inheritanceToRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot) {
	self.xmap joined(appGen, rdbRoot);
}

// inheritanceType = joined
mapping APP::operations::SetParent::joined(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot)
when { appGen.findClass(self.parentName).inheritanceType = APP::InheritanceType::joined; }
{
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.name.toLower();
		name := self.parentName.toLower() + "_id";
		type := RDB::PrimitiveType::int;
	};
	
	// addition of a foreign key to the table
	rdbRoot.operations += object RDB::operations::AddForeignKey {
		owningSchemaName := "public";
		owningTableName := self.name.toLower();
		name := "FK_" + self.name.toLower() + "_" + self.parentName.toLower();
		targetTableName := self.parentName.toLower();
		constrainedColumnName := self.name.toLower() + "_id";
	};
}

mapping APP::operations::MoveProperty::toRdb(appGen : APP::ModelGeneration, inout rdbRoot : RDB::ModelRoot) {

	rdbRoot.operations += object RDB::operations::RenameColumn {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		name := self.name;
		newName := self.name + "_old";
	};
	
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.targetClassName.toLower();
		name := self.name;
		type := RDB::PrimitiveType::boolean;
	};
	
	rdbRoot.operations += object RDB::operations::CopyInstances {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		sourceColumnName := self.name + '_old';
		targetTableName := self.targetClassName.toLower();
		targetColumnName := self.name;
		type := appGen.mergeTypeInAppToMergeTypeInRdb(self.type);
	};
	
	rdbRoot.operations += object RDB::operations::RemoveColumn {
		owningSchemaName := "public";
		owningTableName := self.owningClassName.toLower();
		name := self.name + "_old";		
	};
}


/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

query APP::ModelGeneration::findEntity(eName : String) : APP::Entity {
	return self.classes![name = eName];
}

query APP::ModelGeneration::findClass(cName : String) : APP::Class {
	return self.findEntity(cName).oclAsType(APP::Class);
}

query APP::ModelGeneration::findPrimitiveTypeInApp(pName : String) : APP::PrimitiveType {
	return self.findEntity(pName).oclAsType(APP::PrimitiveClass).primitiveType;
}

query APP::ModelGeneration::findPrimitiveTypeInRdb(pName : String) : RDB::PrimitiveType {
	switch {
		case (self.findPrimitiveTypeInApp(pName) = APP::PrimitiveType::boolean) return RDB::PrimitiveType::boolean;
		case (self.findPrimitiveTypeInApp(pName) = APP::PrimitiveType::char) return RDB::PrimitiveType::char;
		case (self.findPrimitiveTypeInApp(pName) = APP::PrimitiveType::int) return RDB::PrimitiveType::int;
	};
	return null;
}

query APP::ModelGeneration::findMergeTypeInApp(pName : String) : APP::PrimitiveType {
	return self.findEntity(pName).oclAsType(APP::PrimitiveClass).primitiveType;
}

query APP::ModelGeneration::mergeTypeInAppToMergeTypeInRdb(type : APP::operations::MergeType) : RDB::operations::MergeType {
	switch {
		case (type = APP::operations::MergeType::strict) return RDB::operations::MergeType::strict;
		case (type = APP::operations::MergeType::force) return RDB::operations::MergeType::force;
		case (type = APP::operations::MergeType::tolerant) return RDB::operations::MergeType::tolerant;
	};
	return null;
}