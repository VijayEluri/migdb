/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype APP uses app('http://www.collectionspro.eu/jam/mm/app');

transformation APP_To_Rdb(in inModel : APP, out outModel : APP);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(APP::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : APP::ModelRoot = outModel.rootObjects()![APP::ModelRoot];
	var gen0 : APP::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("gen0 " + gen0.repr());
		log("root " + root.repr());
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
			break;
		} endif;
	};
	
	// root.operations := root.operations->reject(true);
}

// a verification of an operation
abstract query APP::operations::ModelOperation::
	isValid(gen : APP::ModelGeneration) : Boolean;

// a mapping of an operation
abstract mapping APP::operations::ModelOperation::
	apply(inout gen : APP::ModelGeneration);


/* ++++++++++ BASIC OPERATIONS ++++++++++++++++++++ */

query APP::operations::AddPrimitiveClass::isValid(gen : APP::ModelGeneration) : Boolean {
	return not gen.isEntityInGeneration(self.name);
}

mapping APP::operations::AddPrimitiveClass::apply(inout gen : APP::ModelGeneration) {
	gen.classes += object APP::PrimitiveClass {
		name := self.name;
		primitiveType := self.primitiveType;
	};
}

query APP::operations::AddEmbeddedClass::isValid(gen : APP::ModelGeneration) : Boolean {
	return not gen.isEntityInGeneration(self.name);
}

mapping APP::operations::AddEmbeddedClass::apply(inout gen : APP::ModelGeneration) {
	gen.classes += object APP::EmbeddedClass {
		name := self.name;
	};
}

query APP::operations::AddClass::isValid(gen : APP::ModelGeneration) : Boolean {
	return not gen.isEntityInGeneration(self.name)
		   and gen.isPrimitiveClassInGeneration("Integer");
}

mapping APP::operations::AddClass::apply(inout gen : APP::ModelGeneration) {	
	gen.classes += object APP::Class {
		name := self.name;
		parent := null;
		isAbstract := self.isAbstract;
		inheritanceType := self.inheritanceType;
		properties += object APP::Property {
			name := "id";
			type := gen.findEntity("Integer"); // gen0 has to contain a primitive class called "Integer"
			lowerBound := 1;
			upperBound := 1;
			isID := true;
			sequenceName := "SQ_" + self.name.toLower();
		};
	};
}

query APP::operations::RenameEntity::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isEntityInGeneration(self.name)
		   and not gen.isEntityInGeneration(self.newName);
}

mapping APP::operations::RenameEntity::apply(inout gen : APP::ModelGeneration) {
	var e : APP::Entity := gen.findEntity(self.name);
	e.name := self.newName;
}

query APP::operations::RemoveEntity::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isEntityInGeneration(self.name)
		   and gen.findEntity(self.name).isRemoveable(gen);
}

query  APP::Entity::isRemoveable(gen : APP::ModelGeneration) : Boolean {
	return false;
}

query APP::PrimitiveClass::isRemoveable(gen : APP::ModelGeneration) : Boolean {
	return not gen.classes->oclAsType(APP::AbstractClass).properties->exists(type = self);
}

query APP::EmbeddedClass::isRemoveable(gen : APP::ModelGeneration) : Boolean {
	return not gen.classes->oclAsType(APP::Class).properties->exists(type = self)
		   and self.properties->isEmpty();
}

query APP::Class::isRemoveable(gen : APP::ModelGeneration) : Boolean {
	return not gen.classes->oclAsType(APP::Class).properties->exists(type = self)
		   and (self.properties->isEmpty() or self.properties->forAll(isID = true))
		   and not gen.classes->oclAsType(APP::Class)->exists(parent = self);
}

mapping APP::operations::RemoveEntity::apply(inout gen : APP::ModelGeneration) {
	gen.classes := gen.classes->excluding(gen.findEntity(self.name));
}

query APP::operations::AddProperty::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and not gen.isPropertyInAbstractClass(self.owningClassName, self.name)
		   and gen.findAbstractClass(self.owningClassName).isTypeValid(gen, self.type)
		   and gen.areBoundsValid(self.lowerBound, self.upperBound);
}

query APP::AbstractClass::isTypeValid(gen : APP::ModelGeneration, type : String) : Boolean {
	if (self.oclIsTypeOf(APP::EmbeddedClass)) then {
		return gen.isPrimitiveClassInGeneration(type);
	} endif;
	if (self.oclIsTypeOf(APP::Class)) then {
		return gen.isPrimitiveClassInGeneration(type) or gen.isAbstractClassInGeneration(type);
	} endif;
	return false;
}

mapping APP::operations::AddProperty::apply(inout gen : APP::ModelGeneration) {
	var c : APP::AbstractClass := gen.findAbstractClass(self.owningClassName);		
	c.properties += object APP::Property {
		name := self.name;
		type := gen.findEntity(self.type);
		defaultValue := self.defaultValue;
		lowerBound := self.lowerBound;
		upperBound := self.upperBound;
		isOrdered := self.isOrdered; // makes sense just for upperBound = -1
		isUnique := self.isUnique; // makes sense just for upperBound = -1
		sequenceName := self.sequenceName;
	};
}

query APP::operations::RenameProperty::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isPropertyInAbstractClass(self.owningClassName, self.name)
		   and not gen.isPropertyInAbstractClass(self.owningClassName, self.newName);
}

mapping APP::operations::RenameProperty::apply(inout gen : APP::ModelGeneration) {
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	p.name := self.newName;
}

query APP::operations::RemoveProperty::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isPropertyInAbstractClass(self.owningClassName, self.name)
		   and gen.findProperty(self.owningClassName, self.name).isID = false
		   and gen.findProperty(self.owningClassName, self.name).oppositeProperty = null;
}

mapping APP::operations::RemoveProperty::apply(inout gen : APP::ModelGeneration) {
	var c : APP::AbstractClass := gen.findAbstractClass(self.owningClassName);
	c.properties := c.properties->excluding(gen.findProperty(self.owningClassName, self.name));
}

query APP::operations::SetOpposite::isValid(gen : APP::ModelGeneration) : Boolean {
	var pre : Boolean := gen.isPropertyInAbstractClass(self.firstClassName, self.firstPropertyName)
						 and gen.isPropertyInAbstractClass(self.secondClassName, self.secondPropertyName);
	if (self.isOpposite = true) then {
		return pre
			   and gen.findProperty(self.firstClassName, self.firstPropertyName).oppositeProperty = null
			   and gen.findProperty(self.secondClassName, self.secondPropertyName).oppositeProperty = null;
	} else {
		return pre
			   and gen.findProperty(self.firstClassName, self.firstPropertyName).oppositeProperty.name = self.secondPropertyName
			   and gen.findProperty(self.secondClassName, self.secondPropertyName).oppositeProperty.name = self.firstPropertyName;
	} endif;
	
	// unreachable statement
	return null;
}

mapping APP::operations::SetOpposite::apply(inout gen : APP::ModelGeneration) {
	var p1 : APP::Property := gen.findProperty(self.firstClassName, self.firstPropertyName);
	var p2 : APP::Property := gen.findProperty(self.secondClassName, self.secondPropertyName);
	if (self.isOpposite = true) then {
		p1.oppositeProperty := p2;
		p2.oppositeProperty := p1;
	} else {
		p1.oppositeProperty := null;
		p2.oppositeProperty := null;
	} endif;	
}

query APP::operations::SetAbstract::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isClassInGeneration(self.name);
}

mapping APP::operations::SetAbstract::apply(inout gen : APP::ModelGeneration) {
	gen.findClass(self.name).isAbstract := self.isAbstract;
}

query APP::operations::SetParent::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isClassInGeneration(self.name)
		   and gen.isClassInGeneration(self.parentName)
		   and not gen.findClass(self.parentName).isDescendantOf(self.name);
}

mapping APP::operations::SetParent::apply(inout gen : APP::ModelGeneration) {
	var c : APP::Class := gen.findClass(self.name);
	
	// tridu odebirame z hierarchie
	if (self.parentName = null) then {
		c.parent := null;
		// vytvoreni ID atributu
		c.properties += object APP::Property {
			name := "id";
			type := gen.findEntity("Integer");
			lowerBound := 1;
			upperBound := 1;
			isID := true;
			sequenceName := "SQ_" + self.name.toLower();
		};
		
	// tridu pridavame do hierarchie
	} else {
		c.parent := gen.findClass(self.parentName);
		// smazani ID atributu
		c.properties := c.properties->excluding(c.properties->selectOne(isID = true));
		
	} endif;
}

/* ++++++++++ SET OPERATIONS FOR PROPERTIES ++++++++++++++++++++ */

query APP::operations::SetType::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInAbstractClass(self.owningClassName, self.name)
		   and gen.isAbstractClassInGeneration(self.type);
}

mapping APP::operations::SetType::apply(inout gen : APP::ModelGeneration) {
	var c : APP::Class := gen.findAbstractClass(self.owningClassName).oclAsType(APP::Class);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	p.type := gen.findAbstractClass(self.type); // null
}

query APP::operations::SetBoundries::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInAbstractClass(self.owningClassName, self.name)
		   // overi, zda-li jsou hranice nastaveny smysluplne
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
}

// nastavi hranice atributu
mapping APP::operations::SetBoundries::apply(inout gen : APP::ModelGeneration) {
	var c : APP::Class := gen.findAbstractClass(self.owningClassName).oclAsType(APP::Class);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	// nastavi dolni hranici atributu
	p.lowerBound := self.lowerBound;
	// nastavi horni hranici atributu
	p.upperBound := self.upperBound;
}

query APP::operations::SetOrdered::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInAbstractClass(self.owningClassName, self.name);
}

mapping APP::operations::SetOrdered::apply(inout gen : APP::ModelGeneration) {
	var c : APP::Class := gen.findAbstractClass(self.owningClassName).oclAsType(APP::Class);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	p.isOrdered := self.isOrdered;
}

query APP::operations::SetUnique::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInAbstractClass(self.owningClassName, self.name);
}

mapping APP::operations::SetUnique::apply(inout gen : APP::ModelGeneration) {
	var c : APP::Class := gen.findAbstractClass(self.owningClassName).oclAsType(APP::Class);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	p.isUnique := self.isUnique;
}


/* ++++++++++ EDIT OPERATIONS ++++++++++++++++++++ */





/* ++++++++++ COMPLEX OPERATIONS ++++++++++++++++++++ */

query APP::operations::CopyProperty::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isPropertyInAbstractClass(self.owningClassName, self.name) 
		   and gen.isClassInGeneration(self.targetClassName)
		   and not gen.isPropertyInAbstractClass(self.targetClassName, self.name)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName)
		   		or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}

mapping APP::operations::CopyProperty::apply(inout gen : APP::ModelGeneration){
	var c : APP::AbstractClass := gen.findAbstractClass(self.targetClassName);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	
	c.properties += object APP::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
	};
}

query APP::operations::MoveProperty::isValid(gen : APP::ModelGeneration) : Boolean{
	return gen.isPropertyInAbstractClass(self.owningClassName, self.name) 
		   and gen.isClassInGeneration(self.targetClassName)
		   and not gen.isPropertyInAbstractClass(self.targetClassName, self.name)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName)
		   		or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}


mapping APP::operations::MoveProperty::apply(inout gen : APP::ModelGeneration){
	var t : APP::AbstractClass := gen.findAbstractClass(self.targetClassName);
	var o : APP::AbstractClass := gen.findAbstractClass(self.owningClassName);
	var p : APP::Property := gen.findProperty(self.owningClassName, self.name);
	
	t.properties += object APP::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
	};
	
	o.properties := o.properties->excluding(p);
}

query APP::operations::ExtractClass::isValid(gen : APP::ModelGeneration) : Boolean {
	return gen.isClassInGeneration(self.sourceClassName)
		   and not gen.isClassInGeneration(self.extractClassName)
		   and gen.arePropertiesInClass(self.sourceClassName, self.extractPropertiesNames); 
}

mapping APP::operations::ExtractClass::apply(inout gen : APP::ModelGeneration) {
	
	gen.modelRoot.operations += object APP::operations::AddClass {
		name := self.extractClassName;
	}.xmap apply(gen);
	
	self.extractPropertiesNames->forEach(pName) {
		var p : APP::Property := gen.findProperty(self.sourceClassName, pName);
		gen.modelRoot.operations += object APP::operations::AddProperty {
			owningClassName := self.extractClassName;
			name := p.name;
			type := p.type.name;
			defaultValue := p.defaultValue;
			lowerBound := p.lowerBound;
			upperBound := p.upperBound;
		}.xmap apply(gen);
		
		gen.modelRoot.operations += object APP::operations::RemoveProperty {
			owningClassName := self.sourceClassName;
			name := p.name;			
		}.xmap apply(gen);
	};
	
	gen.modelRoot.operations += object APP::operations::AddProperty {
			owningClassName := self.sourceClassName;
			name := self.extractClassName.toLower();
			type := self.extractClassName;
			lowerBound := 1;
			upperBound := 1;
	}.xmap apply(gen);
}


/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

query APP::ModelGeneration::isEntityInGeneration(eName : String) : Boolean {
	return self.classes->exists(name = eName);
}

query APP::ModelGeneration::findEntity(eName : String) : APP::Entity {
	return self.classes![name = eName];
}

query APP::ModelGeneration::isPrimitiveClassInGeneration(cName : String) : Boolean {
	return self.findEntity(cName).oclIsTypeOf(APP::PrimitiveClass);
}

query APP::ModelGeneration::findPrimitiveClass(cName : String) : APP::PrimitiveClass {
	return self.findEntity(cName).oclAsType(APP::PrimitiveClass);
}

query APP::ModelGeneration::isAbstractClassInGeneration(cName : String) : Boolean {
	return self.findEntity(cName).oclIsKindOf(APP::AbstractClass);
}

query APP::ModelGeneration::findAbstractClass(cName : String) : APP::AbstractClass {
	return self.findEntity(cName).oclAsType(APP::AbstractClass);
}

query APP::ModelGeneration::isPropertyInAbstractClass(cName : String, pName : String) : Boolean {
	return self.findAbstractClass(cName).properties->exists(name = pName);
}

query APP::ModelGeneration::isPropertyInClass(cName : String, pName : String) : Boolean {
	return self.findClass(cName).properties->exists(name = pName);
}

query APP::ModelGeneration::arePropertiesInClass(cName : String, pNames : OrderedSet(String)) : Boolean {
	return pNames->forAll(name | self.isPropertyInClass(cName, name));
}

query APP::ModelGeneration::findProperty(cName : String, pName : String) : APP::Property {
	return self.findAbstractClass(cName).properties![name = pName];
}

query APP::ModelGeneration::isClassInGeneration(cName : String) : Boolean {
	return self.findEntity(cName).oclIsTypeOf(APP::Class);
}

query APP::ModelGeneration::findClass(cName : String) : APP::Class {
	return self.findEntity(cName).oclAsType(APP::Class);
}

query APP::ModelGeneration::areBoundsValid(lowerBound : Integer, upperBound : Integer) : Boolean {
	return (lowerBound > -1 and (upperBound = -1 or (upperBound > 0 and upperBound >= lowerBound)));
}


// overime, zda-li je trida potomkem tridy cName
query APP::Class::isDescendantOf(cName : String) : Boolean {
	if (self.parent <> null) then {
		if (self.parent.name = cName) then return true
		else return self.parent.isDescendantOf(cName) endif;
	} endif;
	return false;
}

// overi, zda-li je trida sClass rodicem tridy fClass
query APP::ModelGeneration::isInHierarchy(fClass : String, sClass : String) : Boolean {
	var ourClass : APP::Class := self.findAbstractClass(fClass).oclAsType(APP::Class);
	if(ourClass <> null) then {
		if (ourClass.parent <> null and ourClass.parent = sClass) then {
			return true;
		} endif;
		if (ourClass.parent <> null) then {
			return self.isInHierarchy(ourClass.parent.name, sClass);
		} endif;
	}endif;
	return false;
}

// overi, zda-li v hierarchii existuje atribut se stejnem nazvem
query APP::ModelGeneration::isPropertyInHierarchy(cName : String, pName : String) : Boolean {
	// pro kazdy atribut se stejnym nazvem...
	self.classes->oclAsType(APP::Class).properties->forEach(p | p.name = pName) {
		// ...overime, zda-li se tridy, kterym tyto atributy nalezi, nachazi ve stejne hierarchii
		if (self.isInHierarchy(cName, p.owningClass.name) = true or self.isInHierarchy(p.owningClass.name, cName) = true) then {
			return true;
		} endif;
	};
	return false;
}