/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype DB uses 'mm-db';

transformation DB_To_Rdb(in inModel : DB, out outModel : DB);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(DB::rdb::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : DB::rdb::ModelRoot = outModel.rootObjects()![DB::rdb::ModelRoot];
	var gen0 : DB::rdb::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
	
	root.operations = root.operations->reject(true);
}

// kontroluje validitu operace
abstract query DB::rdb::operations::ModelOperation::
	isValid(gen : DB::rdb::ModelGeneration) : Boolean;

// namapuje danou operaci
abstract mapping DB::rdb::operations::ModelOperation::
	apply(inout gen : DB::rdb::ModelGeneration);


/* ++++++++++ ADD OPERATIONS ++++++++++++++++++++ */

query DB::rdb::operations::AddSchema::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dan� generaci neexistuje sch�ma stejn�ho n�zvu
	return not gen.isSchemaInGeneration(self.name);
}

// p�id� sch�ma do generace
mapping DB::rdb::operations::AddSchema::apply(inout gen : DB::rdb::ModelGeneration) {
	gen.schemas += object DB::rdb::Schema {
		name := self.name;
	}
}

query DB::rdb::operations::AddSequence::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dan�m sch�matu neexistuje sekvence stejn�ho n�zvu
	return not gen.isSequenceInSchema(self.owningSchemaName, self.name);
}

// p�id� sekvenci do sch�matu
mapping DB::rdb::operations::AddSequence::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.sequences += object DB::rdb::Sequence {
		name := self.name;
		cacheSize := self.cacheSize;
	}
}

query DB::rdb::operations::AddIndex::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dan�m sch�matu neexistuje index stejn�ho n�zvu
	return not gen.isIndexInSchema(self.owningSchemaName, self.name)
	// v dan� tabulce existuj� dan� sloupce
		   and gen.areColumnsInTable(self.owningSchemaName, self.owningTableName, self.columnsNames);
}

// p�id� index do sch�matu
mapping DB::rdb::operations::AddIndex::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes += object DB::rdb::Index {
		name := self.name;		
		columns += gen.findColumns(self.owningSchemaName, self.owningTableName, self.columnsNames);
	}
}

query DB::rdb::operations::AddTable::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dan�m sch�matu neexistuje tabulka stejn�ho n�zvu
	return not gen.isTableInSchema(self.owningSchemaName, self.name);
}

// p�id� tabulku do sch�matu
mapping DB::rdb::operations::AddTable::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.tables += object DB::rdb::Table {
		name := self.name;
	};
}

query DB::rdb::operations::AddColumn::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dan� tabulce neexistuje sloupec stejn�ho n�zvu
	return not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name);
}

// p�id� sloupec do tabulky
mapping DB::rdb::operations::AddColumn::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.columns += object DB::rdb::Column {
		name := self.name;
		type := self.type;
		defaultValue := self.defaultValue;
	}
}

query DB::rdb::operations::AddUniqueIndex::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezen� stejn�ho n�zvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v dan�m sch�matu existuje dan� index
	and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName);
}

// p�id� omezen� 'UniqueIndex' pro tabulku
mapping DB::rdb::operations::AddUniqueIndex::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object DB::rdb::UniqueIndex {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

query DB::rdb::operations::AddPrimaryKey::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezen� stejn�ho n�zvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v dan�m sch�matu existuje dan� index
	and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName);
}

// p�id� omezen� 'PrimaryKey' pro tabulku
mapping DB::rdb::operations::AddPrimaryKey::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object DB::rdb::PrimaryKey {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

query DB::rdb::operations::AddForeignKey::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// pro danou tabulku neexistuje omezen� stejn�ho n�zvu
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	// v dan�m sch�matu existuje c�lov� tabulka
	and gen.isTableInSchema(self.owningSchemaName, self.targetTableName)
	// v c�lov� tabulce existuj� dan� sloupce
	and gen.areColumnsInTable(self.owningSchemaName, self.targetTableName, self.constrainedColumnNames)
	// pro dan� sloupce existuje omezen� 'Unique'	
	and gen.isUnique(self.owningSchemaName, self.targetTableName, self.targetColumnsNames);
}

// p�id� omezen� 'ForeignKey' pro tabulku
mapping DB::rdb::operations::AddForeignKey::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object DB::rdb::ForeignKey {
		name := self.name;
		targetTable := gen.findTable(self.owningSchemaName, self.targetTableName);
		constrainedColumns := gen.findColumns(self.owningSchemaName, self.targetTableName, self.constrainedColumnNames);
	}
}

query DB::rdb::operations::AddNotNullConstraint::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// pro dan� sloupec neexistuje omezen� stejn�ho n�zvu
	return not gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);	   
}

// p�id� omezen� 'NotNull' pro sloupec
mapping DB::rdb::operations::AddNotNullConstraint::apply(inout gen : DB::rdb::ModelGeneration) {
	var c : DB::rdb::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.constraints += object DB::rdb::NotNullConstraint {
		name := self.name;
	}
}

/* ++++++++++ EDIT OPERATIONS ++++++++++++++++++++ */

query DB::rdb::operations::RenameTable::isValid( gen : DB::rdb::ModelGeneration) : Boolean {
	// tabulka s n�zvem @name existuje ve sch�matu
	return gen.isTableInSchema(self.owningSchemaName, self.name)
	// tabulka s n�zvem @newName neexistuje ve sch�matu
		   and not gen.isTableInSchema(self.owningSchemaName, self.newName);
}

mapping DB::rdb::operations::RenameTable::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.name);
	// zm�na hodnoty atributu name
	t.name := self.newName;
}

query DB::rdb::operations::RenameColumn::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// sloupec s n�zvem @name existuje v tabulce
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
	// sloupec s n�zvem @newName neexistuje v tabulce
		   and not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.newName);
}

mapping DB::rdb::operations::RenameColumn::apply(inout gen : DB::rdb::ModelGeneration) {
	var c : DB::rdb::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	// zm�na hodnoty atributu name
	c.name := self.newName;
}


/* ++++++++++ REMOVE OPERATIONS ++++++++++++++++++++ */

query DB::rdb::operations::RemoveTable::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
   
}

// odstran� tabulku z dan�ho sch�matu
mapping DB::rdb::operations::RemoveTable::apply(inout gen : DB::rdb::ModelGeneration) {
	
}

query DB::rdb::operations::RemoveColumn::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
    // sloupec existuje v dan� tabulce
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
	// pro dan� sloupec neexistuje omezen� 'PrimaryKey' �i 'ForeignKey'
		   and not gen.isKey(self.owningSchemaName, self.owningTableName, self.name);
}

// odstran� atribut z dan� t��dy
mapping DB::rdb::operations::RemoveColumn::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	// odstran�n� sloupce
	t.columns := t.columns->excluding(gen.findColumn(self.owningSchemaName, self.owningTableName, self.name));
}

query DB::rdb::operations::RemoveTableConstraint::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
    // omezen� existuje pro danou tabulku
	return gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name);
}

// odstran� omezen� pro danou tabulku
mapping DB::rdb::operations::RemoveTableConstraint::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	// odstran�n� omezen�
	t.constraints := t.constraints->excluding(gen.findTableConstraint(self.owningSchemaName, self.owningTableName, self.name));
}

query DB::rdb::operations::RemoveColumnConstraint::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
    // omezen� existuje pro dan� sloupec
	return gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);
}

// odstran� omezen� pro dan� sloupec
mapping DB::rdb::operations::RemoveColumnConstraint::apply(inout gen : DB::rdb::ModelGeneration) {
	var c : DB::rdb::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	// odstran�n� omezen�
	c.constraints := c.constraints->excluding(gen.findColumnConstraint(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name));
}

query DB::rdb::operations::RemoveIndex::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
    // index existuje v dan�m sch�matu
	return gen.isIndexInSchema(self.owningSchemaName, self.name);
}

// odstran� index z dan�ho sch�matu
mapping DB::rdb::operations::RemoveIndex::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	// odstran�n� index
	s.indexes := s.indexes->excluding(gen.findIndex(self.owningSchemaName, self.name));
}

/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

// ov���, zda-li existuje sch�ma v generaci
query DB::rdb::ModelGeneration::isSchemaInGeneration(sName : String) : Boolean {
	return self.schemas->exists(name = sName);
}

// vr�t� sch�ma z generace
query DB::rdb::ModelGeneration::findSchema(sName : String) : DB::rdb::Schema {
	return self.schemas->selectOne(name = sName);
}

// ov���, zda-li existuje sekvence ve sch�matu
query DB::rdb::ModelGeneration::isSequenceInSchema(sName : String, qName : String) : Boolean {
	return self.findSchema(sName).sequences->exists(name = qName);
}

// ov���, zda-li existuje index ve sch�matu
query DB::rdb::ModelGeneration::isIndexInSchema(sName : String, iName : String) : Boolean {
	return self.findSchema(sName).indexes->exists(name = iName);
}

// vr�t� index ze sch�ma
query DB::rdb::ModelGeneration::findIndex(sName : String, iName : String) : DB::rdb::Index {
	return self.findSchema(sName).indexes->selectOne(name = iName);
}

// ov���, zda-li existuje tabulka ve sch�matu
query DB::rdb::ModelGeneration::isTableInSchema(sName : String, tName : String) : Boolean {
	return self.findSchema(sName).tables->exists(name = tName);
}

// vr�t� tabulku ze sch�ma
query DB::rdb::ModelGeneration::findTable(sName : String, tName : String) : DB::rdb::Table {
	return self.findSchema(sName).tables->selectOne(name = tName);
}

// ov���, zda-li existuje sloupec v tabulce
query DB::rdb::ModelGeneration::isColumnInTable(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).columns->exists(name = cName);
}

// ov���, zda-li existuj� sloupce v tabulce
query DB::rdb::ModelGeneration::areColumnsInTable(sName : String, tName : String, cNames : OrderedSet(String)) : Boolean {
	var b : Boolean := true;
	cNames->forEach(cName) {
		b = b and self.isColumnInTable(sName, tName, cName);
	};
	return b;
}

// vr�t� sloupec z tabulky
query DB::rdb::ModelGeneration::findColumn(sName : String, tName : String, cName : String) : DB::rdb::Column {
	return self.findTable(sName, tName).columns->selectOne(name = cName);
}

// vr�t� se�azenou mno�inu sloupc� z tabulky
query DB::rdb::ModelGeneration::findColumns(sName : String, tName : String, cNames : OrderedSet(String)) : OrderedSet(DB::rdb::Column) {
	var columns : OrderedSet(DB::rdb::Column);
	cNames->forEach(cName) {
		columns += self.findColumn(sName, tName, cName);
	};
	return columns;
}

// ov���, zda-li existuje omezen� pro tabulku
query DB::rdb::ModelGeneration::isConstraintForTable(sName : String, tName : String, rName : String) : Boolean {
	return self.findTable(sName, tName).constraints->exists(name = rName);
}

// ov���, zda-li existuje omezen� pro sloupec 
query DB::rdb::ModelGeneration::isConstraintForColumn(sName : String, tName : String, cName : String, rName : String) : Boolean {
	return self.findColumn(sName, tName, cName).constraints->exists(name = rName);
}

// vr�t� omezen� dan� tabulky
query DB::rdb::ModelGeneration::findTableConstraint(sName : String, tName : String, rName : String) : DB::rdb::TableConstraint {
	return self.findTable(sName, tName).constraints->selectOne(name = rName);
}

// vr�t� omezen� dan�ho sloupce
query DB::rdb::ModelGeneration::findColumnConstraint(sName : String, tName : String, cName : String, rName : String) : DB::rdb::ColumnConstraint {
	return self.findColumn(sName, tName, cName).constraints->selectOne(name = rName);
}

// ov���, zda-li existuje pro sloupce tabulkov� omezen� 'Unique' �i 'PrimaryKey' 
query DB::rdb::ModelGeneration::isUnique(sName : String, tName : String, cName : OrderedSet(String)) : Boolean {
	var target : OrderedSet(DB::rdb::Column) = self.findColumns(sName, tName, cName);
	// vybere omezen� typu 'Unique'
	self.findTable(sName, tName).constraints.oclAsType(DB::rdb::UniqueIndex)->forEach(r) {
		if (r.underlyingIndex.columns = target) then {
			return true;
		} endif;
	};
	return false;
}

// ov���, zda-li existuje pro sloupce tabulkov� omezen� 'PrimaryKey' �i 'ForeignKey' 
query DB::rdb::ModelGeneration::isKey(sName : String, tName : String, cName : String) : Boolean {
	var target : DB::rdb::Column = self.findColumn(sName, tName, cName);
	// vybere omezen� typu 'PrimaryKey'
	self.findTable(sName, tName).constraints.oclAsType(DB::rdb::PrimaryKey)->forEach(r) {
		r.underlyingIndex.columns->forEach(c) {
			if (c = target) then {
				return true;
			} endif;
		};
	};
	// vybere omezen� typu 'ForeignKey'
	self.findTable(sName, tName).constraints.oclAsType(DB::rdb::ForeignKey)->forEach(r) {
		r.targetColumns->forEach(c) {
			if (c = target) then {
				return true;
			} endif;
		};
	};
	return false;
}