/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype DB uses 'mm-db';

transformation DB_To_Rdb(in inModel : DB, out outModel : DB);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(DB::rdb::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : DB::rdb::ModelRoot = outModel.rootObjects()![DB::rdb::ModelRoot];
	var gen0 : DB::rdb::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
	
	root.operations = root.operations->reject(true);
}

// kontroluje validitu operace
abstract query DB::rdb::operations::ModelOperation::
	isValid(gen : DB::rdb::ModelGeneration) : Boolean;

// namapuje danou operaci
abstract mapping DB::rdb::operations::ModelOperation::
	apply(inout gen : DB::rdb::ModelGeneration);


/* ++++++++++ ADD OPERATIONS ++++++++++++++++++++ */

query DB::rdb::operations::AddSchema::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dané generaci neexistuje schéma stejného názvu
	return not gen.isSchemaInGeneration(self.name);
}

// pøidá schéma do generace
mapping DB::rdb::operations::AddSchema::apply(inout gen : DB::rdb::ModelGeneration) {
	gen.schemas += object DB::rdb::Schema {
		name := self.name;
	}
}

query DB::rdb::operations::AddSequence::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v daném schématu neexistuje sekvence stejného názvu
	return not gen.isSequenceInSchema(self.owningSchemaName, self.name);
}

// pøidá sekvenci do schématu
mapping DB::rdb::operations::AddSequence::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.sequences += object DB::rdb::Sequence {
		name := self.name;
		cacheSize := self.cacheSize;
	}
}

query DB::rdb::operations::AddIndex::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v daném schématu neexistuje index stejného názvu
	return not gen.isIndexInSchema(self.owningSchemaName, self.name);
}

// pøidá index do schématu
mapping DB::rdb::operations::AddIndex::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes += object DB::rdb::Index {
		name := self.name;
	}
}

query DB::rdb::operations::AddTable::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v daném schématu neexistuje tabulka stejného názvu
	return not gen.isTableInSchema(self.owningSchemaName, self.name);
}

// pøidá tabulku do schématu
mapping DB::rdb::operations::AddTable::apply(inout gen : DB::rdb::ModelGeneration) {
	var s : DB::rdb::Schema := gen.findSchema(self.owningSchemaName);
	s.tables += object DB::rdb::Table {
		name := self.name;
	};
}

query DB::rdb::operations::AddColumn::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// v dané tabulce neexistuje sloupec stejného názvu
	return not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name);
}

// pøidá sloupec do tabulky
mapping DB::rdb::operations::AddColumn::apply(inout gen : DB::rdb::ModelGeneration) {
	var t : DB::rdb::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.columns += object DB::rdb::Column {
		name := self.name;
		type := self.type;
		defaultValue := self.defaultValue;
	}
}

query DB::rdb::operations::AddPrimaryKey::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	
}

// pøidá omezení 'PrimaryKey' pro tabulku
mapping DB::rdb::operations::AddPrimaryKey::apply(inout gen : DB::rdb::ModelGeneration) {
	
}

query DB::rdb::operations::AddForeignKey::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	
}

// pøidá omezení 'ForeignKey' pro tabulku
mapping DB::rdb::operations::AddForeignKey::apply(inout gen : DB::rdb::ModelGeneration) {
	
}

query DB::rdb::operations::AddNotNullConstraint::isValid(gen : DB::rdb::ModelGeneration) : Boolean {
	// pro daný sloupec neexistuje omezení stejného názvu
	return not gen.isConstrainForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);	   
}

// pøidá omezení 'NotNull' pro sloupec
mapping DB::rdb::operations::AddNotNullConstraint::apply(inout gen : DB::rdb::ModelGeneration) {
	var c : DB::rdb::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.constraints += object DB::rdb::NotNullConstraint {
		name := self.name;
	}
}

/* ++++++++++ SET OPERATIONS ++++++++++++++++++++ */




/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

// ovìøí, zda-li existuje schéma v generaci
query DB::rdb::ModelGeneration::isSchemaInGeneration(sName : String) : Boolean {
	return self.schemas->exists(name = sName);
}

// vrátí schéma z generace
query DB::rdb::ModelGeneration::findSchema(sName : String) : DB::rdb::Schema {
	return self.schemas->selectOne(name = sName);
}

// ovìøí, zda-li existuje sekvence ve schématu
query DB::rdb::ModelGeneration::isSequenceInSchema(sName : String, qName : String) : Boolean {
	return self.findSchema(sName).sequences->exists(name = qName);
}

// ovìøí, zda-li existuje index ve schématu
query DB::rdb::ModelGeneration::isIndexInSchema(sName : String, iName : String) : Boolean {
	return self.findSchema(sName).indexes->exists(name = iName);
}

// ovìøí, zda-li existuje tabulka ve schématu
query DB::rdb::ModelGeneration::isTableInSchema(sName : String, tName : String) : Boolean {
	return self.findSchema(sName).tables->exists(name = tName);
}

// vrátí tabulku ze schéma
query DB::rdb::ModelGeneration::findTable(sName : String, tName : String) : DB::rdb::Table {
	return self.findSchema(sName).tables->selectOne(name = tName);
}

// ovìøí, zda-li existuje sloupec v tabulce
query DB::rdb::ModelGeneration::isColumnInTable(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).columns->exists(name = cName);
}

// vrátí sloupec z tabulky
query DB::rdb::ModelGeneration::findColumn(sName : String, tName : String, cName : String) : DB::rdb::Column {
	return self.findTable(sName, tName).columns->selectOne(name = cName);
}

// ovìøí, zda-li existuje omezení pro sloupec 
query DB::rdb::ModelGeneration::isConstrainForColumn(sName : String, tName : String, cName : String, rName : String) : Boolean {
	return self.findColumn(sName, tName, cName).constraints->exists(name = rName);
}