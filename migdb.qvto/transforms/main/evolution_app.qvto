/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */
import queries_app;
import builder_app;

modeltype APP uses app('http://www.collectionspro.eu/jam/mm/app');
library evolution_app;

// a verification of an operation
abstract query APP::ops::ModelOperation::
	isValid(root : APP::Structure) : Boolean;

// a mapping of an operation
abstract helper APP::ops::ModelOperation::
	apply(inout root : APP::Structure);

// a dekompose of an complex operation to set of atomic operations
abstract query APP::ops::DecomposableOperation::
	decompose(inout appStructure : APP::Structure) : OrderedSet(app::ops::AtomicOperation);

/* ++++++++++ BASIC OPERATIONS ++++++++++++++++++++ */

query APP::ops::AddPrimitiveClass::isValid(root : APP::Structure) : Boolean {
	return not root.containsEntity(self.name);
}

helper APP::ops::AddPrimitiveClass::apply(inout root : APP::Structure) {
	root.entities += _primitiveClass(self.name, self.primitiveType);
	return;
}

query APP::ops::AddEmbeddedClass::isValid(root : APP::Structure) : Boolean {
	return not root.containsEntity(self.name);
}

helper APP::ops::AddEmbeddedClass::apply(inout root : APP::Structure) {
	root.entities += _embeddedClass(self.name, _idProperty(root.findDefaultIdType()));
	return;
}

query APP::ops::AddStandardClass::isValid(root : APP::Structure) : Boolean {
	return not root.containsEntity(self.name) and root.containsPrimitiveClass(getAppDefaultIdTypeName());
}

helper APP::ops::AddStandardClass::apply(inout root : APP::Structure) {	
	root.entities += _class(self.name, _idProperty(root.findDefaultIdType()), self.isAbstract, self.inheritanceType);
	return;
}

query APP::ops::RenameEntity::isValid(root : APP::Structure) : Boolean {
	return root.containsEntity(self.name) and not root.containsEntity(self.newName);
}

helper APP::ops::RenameEntity::apply(inout root : APP::Structure) {
	var entity : APP::ModelEntity := root.findEntity(self.name);
	entity.name := self.newName;
	return;
}

query APP::ops::RemoveEntity::isValid(root : APP::Structure) : Boolean {
	return root.containsEntity(self.name)
		   and root.findEntity(self.name).isRemoveable(root);
}

query  APP::ModelEntity::isRemoveable(root : APP::Structure) : Boolean {
	return false;
}

query APP::PrimitiveClass::isRemoveable(root : APP::Structure) : Boolean {
	return not root.entities->oclAsType(APP::GeneralClass).properties->exists(type = self);
}

query APP::EmbeddedClass::isRemoveable(root : APP::Structure) : Boolean {
	return not root.entities->oclAsType(APP::StandardClass).properties->exists(type = self)
		   and self.properties->isEmpty();
}

query APP::StandardClass::isRemoveable(root : APP::Structure) : Boolean {
	return not root.entities->oclAsType(APP::StandardClass).properties->exists(type = self)
		   and (self.properties->isEmpty() or self.properties->forAll(isID()))
		   and not root.entities[APP::StandardClass]->exists(parent = self);
}

helper APP::ops::RemoveEntity::apply(inout root : APP::Structure) {
	root.entities := root.entities->excluding(root.findEntity(self.name));
	return;
}

query APP::ops::AddProperty::isValid(root : APP::Structure) : Boolean {
	return root.containsGeneralClass(self.owningClassName)
		   and not root.isPropertyInGeneralClass(self.owningClassName, self.name)
		   and root.findGeneralClass(self.owningClassName).isTypeValid(root, self.type)
		   and root.areBoundsValid(self.lowerBound, self.upperBound);
}

query APP::GeneralClass::isTypeValid(root : APP::Structure, type : String) : Boolean {
	if (self.oclIsTypeOf(APP::EmbeddedClass)) then {
		return root.containsPrimitiveClass(type);
	} endif;
	if (self.oclIsTypeOf(APP::StandardClass)) then {
		return root.containsPrimitiveClass(type) or root.containsGeneralClass(type);
	} endif;
	return false;
}

helper APP::ops::AddProperty::apply(inout root : APP::Structure) {
	var owningClass : APP::GeneralClass := root.findGeneralClass(self.owningClassName);
	var typeEnity : APP::ModelEntity := root.findEntity(self.type);
	owningClass.properties += _property(self.name, typeEnity, self.defaultValue, self.lowerBound, self.upperBound, self.isOrdered, self.isUnique);
	return null;
}



query APP::ops::RenameProperty::isValid(root : APP::Structure) : Boolean {
	return root.isPropertyInGeneralClass(self.owningClassName, self.name)
		   and not root.isPropertyInGeneralClass(self.owningClassName, self.newName);
}

helper APP::ops::RenameProperty::apply(inout root : APP::Structure) {
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	p.name := self.newName;
	return;
}

query APP::ops::RemoveProperty::isValid(root : APP::Structure) : Boolean {
	return root.isPropertyInGeneralClass(self.owningClassName, self.name)
		   and not root.findProperty(self.owningClassName, self.name).isID()
		   and root.findProperty(self.owningClassName, self.name).oppositeProperty = null;
}

helper APP::ops::RemoveProperty::apply(inout root : APP::Structure) {
	var c : APP::GeneralClass := root.findGeneralClass(self.owningClassName);
	c.properties := c.properties->excluding(root.findProperty(self.owningClassName, self.name));
	return;
}

query APP::ops::SetOpposite::isValid(root : APP::Structure) : Boolean {
	var pre : Boolean := root.isPropertyInGeneralClass(self.firstClassName, self.firstPropertyName)
						 and root.isPropertyInGeneralClass(self.secondClassName, self.secondPropertyName);
	if (self.isOpposite = true) then {
		return pre
			   and root.findProperty(self.firstClassName, self.firstPropertyName).oppositeProperty = null
			   and root.findProperty(self.secondClassName, self.secondPropertyName).oppositeProperty = null;
	} else {
		return pre
			   and root.findProperty(self.firstClassName, self.firstPropertyName).oppositeProperty.name = self.secondPropertyName
			   and root.findProperty(self.secondClassName, self.secondPropertyName).oppositeProperty.name = self.firstPropertyName;
	} endif;
	
	// unreachable statement
	return null;
}

helper APP::ops::SetOpposite::apply(inout root : APP::Structure) {
	var p1 : APP::Property := root.findProperty(self.firstClassName, self.firstPropertyName);
	var p2 : APP::Property := root.findProperty(self.secondClassName, self.secondPropertyName);
	if (self.isOpposite = true) then {
		p1.oppositeProperty := p2;
		p2.oppositeProperty := p1;
	} else {
		p1.oppositeProperty := null;
		p2.oppositeProperty := null;
	} endif;	
}

query APP::ops::SetAbstract::isValid(root : APP::Structure) : Boolean {
	return root.containsEntity(self.name);
}

helper APP::ops::SetAbstract::apply(inout root : APP::Structure) {
	root.findClass(self.name).isAbstract := self.isAbstract;
	return; 
}

query APP::ops::SetParent::isValid(root : APP::Structure) : Boolean {
	return root.containsEntity(self.name)
		   and root.containsEntity(self.parentName)
		   and not root.findClass(self.parentName).isDescendantOf(self.name);
}

helper APP::ops::SetParent::apply(inout root : APP::Structure) {
	var c : APP::StandardClass := root.findClass(self.name);
	
	// tridu odebirame z hierarchie
	if (self.parentName = null) then {
		c.parent := null;
		// vytvoreni ID atributu
		var idProp : Property := _idProperty( root.findPrimitiveClass(getAppDefaultIdTypeName()));
		c.properties += idProp;
		c.idProperty := idProp;
		
	// tridu pridavame do hierarchie
	} else {
		c.parent := root.findClass(self.parentName);
		// smazani ID atributu
		c.properties := c.properties->excluding(c.idProperty);
		c.idProperty := null;
	} endif;
}

/* ++++++++++ SET OPERATIONS FOR PROPERTIES ++++++++++++++++++++ */
query APP::ops::SetType::isValid(root : APP::Structure) : Boolean {
	return root.containsGeneralClass(self.owningClassName)
		   and root.isPropertyInGeneralClass(self.owningClassName, self.name)
		   and root.containsGeneralClass(self.type);
}

helper APP::ops::SetType::apply(inout root : APP::Structure) {
	var c : APP::StandardClass := root.findGeneralClass(self.owningClassName).oclAsType(APP::StandardClass);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	p.type := root.findGeneralClass(self.type); // null
	return;
}

query APP::ops::SetBounds::isValid(root : APP::Structure) : Boolean {
	return root.containsGeneralClass(self.owningClassName)
		   and root.isPropertyInGeneralClass(self.owningClassName, self.name)
		   // overi, zda-li jsou hranice nastaveny smysluplne
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
}

// nastavi hranice atributu
helper APP::ops::SetBounds::apply(inout root : APP::Structure) {
	var c : APP::StandardClass := root.findGeneralClass(self.owningClassName).oclAsType(APP::StandardClass);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	// nastavi dolni hranici atributu
	p.lowerBound := self.lowerBound;
	// nastavi horni hranici atributu
	p.upperBound := self.upperBound;
	return;
}

query APP::ops::SetOrdered::isValid(root : APP::Structure) : Boolean {
	return root.containsGeneralClass(self.owningClassName)
		   and root.isPropertyInGeneralClass(self.owningClassName, self.name);
}

helper APP::ops::SetOrdered::apply(inout root : APP::Structure) {
	var c : APP::StandardClass := root.findGeneralClass(self.owningClassName).oclAsType(APP::StandardClass);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	p.isOrdered := self.isOrdered;
	return;
}

query APP::ops::SetUnique::isValid(root : APP::Structure) : Boolean {
	return root.containsGeneralClass(self.owningClassName)
		   and root.isPropertyInGeneralClass(self.owningClassName, self.name);
}

helper APP::ops::SetUnique::apply(inout root : APP::Structure) {
	var c : APP::StandardClass := root.findGeneralClass(self.owningClassName).oclAsType(APP::StandardClass);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	p.isUnique := self.isUnique;
	return;
}

/* ++++++++++ COMPLEX OPERATIONS ++++++++++++++++++++ */

query APP::ops::CopyProperty::isValid(root : APP::Structure) : Boolean {
	return root.isPropertyInGeneralClass(self.owningClassName, self.name) 
		   and root.containsEntity(self.targetClassName)
		   and not root.isPropertyInGeneralClass(self.targetClassName, self.name)
		   and root.areClassesRelatives(self.owningClassName, self.targetClassName);
}

helper APP::ops::CopyProperty::apply(inout root : APP::Structure){
	var c : APP::GeneralClass := root.findGeneralClass(self.targetClassName);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	c.properties += _property(p.name, p.type, p.defaultValue, p.lowerBound, p.upperBound, p.isOrdered, p.isUnique);
	return;
}

query APP::ops::MoveProperty::isValid(root : APP::Structure) : Boolean{
	
	return root.isPropertyInGeneralClass(self.owningClassName, self.name) 
		   and root.containsEntity(self.targetClassName)
		   and not root.isPropertyInGeneralClass(self.targetClassName, self.name)
		   and root.areClassesRelatives(self.owningClassName, self.targetClassName);
}


mapping APP::ops::MoveProperty::apply(inout root : APP::Structure){
	var t : APP::GeneralClass := root.findGeneralClass(self.targetClassName);
	var o : APP::GeneralClass := root.findGeneralClass(self.owningClassName);
	var p : APP::Property := root.findProperty(self.owningClassName, self.name);
	
	//unsetting property from o is done by asserting p to collection in t
	t.properties += p;	
	//t.properties += _property(p.name, p.type, p.defaultValue, p.lowerBound, p.upperBound, p.isOrdered, p.isUnique);
	//o.properties := o.properties->excluding(p);
}

query APP::ops::ExtractClass::decompose(inout appStructure : APP::Structure) : OrderedSet(app::ops::AtomicOperation){
	var sourceClass : StandardClass := appStructure.findStandardClass(self.sourceClassName);
	var operations : OrderedSet(app::ops::AtomicOperation) = OrderedSet{_addClass(self.extractClassName, false, sourceClass.inheritanceType)};
	
	self.extractPropertiesNames->forEach( pName) {
		var p : APP::Property := appStructure.findProperty(self.sourceClassName, pName);
		operations += object APP::ops::AddProperty {
			owningClassName := self.extractClassName;
			name := p.name;
			type := p.type.name;
			defaultValue := p.defaultValue;
			lowerBound := p.lowerBound;
			upperBound := p.upperBound;
		};
		
		operations += object APP::ops::RemoveProperty {
			owningClassName := self.sourceClassName;
			name := p.name;			
		};
	};
	
	operations += object APP::ops::AddProperty {
			owningClassName := self.sourceClassName;
			name := self.extractClassName.toLower();
			type := self.extractClassName;
			lowerBound := 1;
			upperBound := 1;
	};
		return operations;
}