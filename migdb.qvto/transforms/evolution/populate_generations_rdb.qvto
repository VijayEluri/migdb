modeltype RDB uses rdb('http://www.collectionspro.eu/jam/mm/rdb');

transformation DB_To_Rdb(in inModel : RDB, out outModel : RDB);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(RDB::ModelRoot)));

	log("RDB evolution started...");

	inModel.rootObjects().deepclone();

	var root : RDB::ModelRoot = outModel.rootObjects()![RDB::ModelRoot];
	var gen0 : RDB::ModelGeneration = root.modelGenerations->first();

	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
			break;
		} endif;
	};
}

abstract query RDB::operations::ModelOperation::
	isValid(gen : RDB::ModelGeneration) : Boolean;

abstract mapping RDB::operations::ModelOperation::
	apply(inout gen : RDB::ModelGeneration);


/********************************************************************************
 *								ADD OPERATIONS									*
 ********************************************************************************/


/**
 * ADD SCHEMA
 * Operation create new database schema in generation model
 * @restriction : schema with @name must not exists in generation
 */
query RDB::operations::AddSchema::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isSchemaInGeneration(self.name);
}

mapping RDB::operations::AddSchema::apply(inout gen : RDB::ModelGeneration) {
	gen.schemas += object RDB::Schema {
		name := self.name;
	}
}

/**
 * ADD SEQUENCE
 * we can use only one global sequence in the system. 
 * Operation is something like singleton - Sequence can be created only one times.
 * @restriction : sequence with @newDefaultValue must not exists in generation
 * @restriction : in the schema must not be other sequence
 */
query RDB::operations::AddSequence::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isSequenceInSchema(self.owningSchemaName, self.name) 
		and gen.findSchema(self.owningSchemaName)->sequences->size() = 0;
}

mapping RDB::operations::AddSequence::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.sequences += object RDB::Sequence {
		name := self.name;
		startValue := self.startValue;
	}
}

/**
 * ADD INDEX
 * Create index in defined schema.
 * @restriction : in the schema must not be an index with the same @name.
 * @restriction : columns with @columnsNames must exists in table
 */
query RDB::operations::AddIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isIndexInSchema(self.owningSchemaName, self.name)
		and gen.areColumnsInTable(self.owningSchemaName, self.owningTableName, self.columnsNames);
}

mapping RDB::operations::AddIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes += object RDB::Index {
		name := self.name;			
		columns += gen.findColumns(self.owningSchemaName, self.owningTableName, self.columnsNames);
	}
}

/**
 * ADD TABLE
 * Create table in defined schema.
 * @restriction : in the schema must not be a table with the same @name.
 */
query RDB::operations::AddTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isTableInSchema(self.owningSchemaName, self.name);
}

mapping RDB::operations::AddTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.tables += object RDB::Table {
		name := self.name;
	};
}

/**
 * ADD COLUMN
 * Create column in defined table.
 * @restriction : in the table must not be a column with the same @name.
 */
query RDB::operations::AddColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name);
}

mapping RDB::operations::AddColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.columns += object RDB::Column {
		name := self.name;
		type := self.type;
		defaultValue := self.defaultValue;
	}
}

/**
 * ADD UNIQUE INDEX
 * Create unique constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : index with @underlyingIndexName must exists in schema
 * @restriction : column with @columnName must exists in table
 */
query RDB::operations::AddUniqueIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName)
		and gen.areColumnsInTable(self.owningSchemaName, self.owningTableName, self.columnsNames);
}

mapping RDB::operations::AddUniqueIndex::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::UniqueIndex {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

/**
 * ADD PRIMARY KEY
 * Create primary key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : index with @underlyingIndexName must exists in schema
 * @restriction : column with @columnName must exists in table
 */
query RDB::operations::AddPrimaryKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isIndexInSchema(self.owningSchemaName, self.underlyingIndexName)
		and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.columnName);
}

mapping RDB::operations::AddPrimaryKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::PrimaryKey {
		name := self.name;
		underlyingIndex := gen.findIndex(self.owningSchemaName, self.underlyingIndexName);
	}
}

/**
 * ADD FOREIGN KEY
 * Create foreign key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : column with @constrainedColumnName must exists in table
 * @restriction : table with @targetTableName must exists in schema.
 */
query RDB::operations::AddForeignKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.constrainedColumnName)
	and gen.isTableInSchema(self.owningSchemaName, self.targetTableName);
}

mapping RDB::operations::AddForeignKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += object RDB::ForeignKey {
		name := self.name;
		targetTable := gen.findTable(self.owningSchemaName, self.targetTableName);
		constrainedColumn := gen.findColumn(self.owningSchemaName, self.owningTableName, self.constrainedColumnName);
	}
}

/**
 * ADD FOREIGN KEY
 * Create foreign key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : column with @owningColumnName must exists in table
 */
query RDB::operations::AddNotNullConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name)
		and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);	   
}

mapping RDB::operations::AddNotNullConstraint::apply(inout gen : RDB::ModelGeneration) {
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.constraints += object RDB::NotNullConstraint {
		name := self.name;
	}
}

/********************************************************************************
 *								SET OPERATIONS									*
 ********************************************************************************/

/**
 * RENAME TABLE
 * Create new name for table.
 * @restriction : the table must exists in schema
 * @restriction : in the schema must not be a table with the same @newName.
 */
query RDB::operations::RenameTable::isValid( gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.name)
		and not gen.isTableInSchema(self.owningSchemaName, self.newName);
}

mapping RDB::operations::RenameTable::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.name);
	t.name := self.newName;
}

/**
 * RENAME COLUMN
 * Create new name for column.
 * @restriction : the column must exists in table
 * @restriction : in the table must not be a column with the same @newName.
 */
query RDB::operations::RenameColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.newName);
}

mapping RDB::operations::RenameColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	t.constraints->forEach(r) {
		if (r.oclAsType(RDB::ForeignKey).constrainedColumn.name = c.name.toLower()) then {
			r.oclAsType(RDB::ForeignKey).name := r.oclAsType(RDB::ForeignKey).name.replace("_" + self.name + "_", "_" + self.newName + "_");
		} endif;
	};
	c.name := self.newName;
}

/**
 * SET COLUMN TYPE
 * This operation replace old type of column with a new type of column
 * Column and new type must exists in actual model generation. 
 * @restriction : column with @name must exists in table
 */
query RDB::operations::SetColumnType::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);
}

mapping RDB::operations::SetColumnType::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.type := self.newType;
}

/**
 * SET COLUMN DEFAULT VALUE
 * This operation set new default value to column
 * Column must exists in actual model generation. 
 * If is property isSequence set on TRUE - Sequence must exist in generation - this information is used only in SQL generator
 * In sequence case @newDefaultValue means name of sequence in model generation
 * @restriction : column with @owningColumnName must exists in table
 */
query RDB::operations::SetColumnDefaultValue::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);
}

mapping RDB::operations::SetColumnDefaultValue::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.defaultValue := self.newDefaultValue;
}



/********************************************************************************
 *							 REMOVE OPERATIONS									*
 ********************************************************************************/

/**
 * REMOVE TABLE
 * This operation remove table from schema and generation.
 * @restriction : table with @name must exists in generation
 * @restriction : table must not have columns
 * @restriction : table must not have table constraints
 */
query RDB::operations::RemoveTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.name)
		   and gen.findTable(self.owningSchemaName, self.name).columns->size() = 0
		   and gen.findTable(self.owningSchemaName, self.name).constraints->size() = 0;
}

mapping RDB::operations::RemoveTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.tables := s.tables->excluding(gen.findTable(self.owningSchemaName, self.name));
}

/**
 * REMOVE COLUMN
 * This operation remove column from table and generation.
 * ForeignKey constraint will be remove with column. 
 * @restriction : column with @name must exists in table
 * @restriction : column mustn't be part of Unique constraint
 * @restriction : column mustn't be part of PrimaryKey constraint
 * @restriction : column mustn't be part of NotNull constraint
 */
query RDB::operations::RemoveColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isUnique(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isPrimaryKey(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isNotNull(self.owningSchemaName, self.owningTableName, self.name);
}

mapping RDB::operations::RemoveColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	t.constraints->forEach(r) {
		if (r.oclAsType(RDB::ForeignKey).constrainedColumn.name = c.name.toLower()) then {
			t.constraints := t.constraints->excluding(r);
		} endif;
	};
	t.columns := t.columns->excluding(c);
}

/**
 * REMOVE TABLE CONSTRAINT
 * This operation remove table constraint from table
 * @restriction : table constraint with @name must exists in table 
 */
query RDB::operations::RemoveTableConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name);
}

mapping RDB::operations::RemoveTableConstraint::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints := t.constraints->excluding(gen.findTableConstraint(self.owningSchemaName, self.owningTableName, self.name));
}

/**
 * REMOVE COLUMN CONSTRAINT
 * This operation remove column constraint from column
 * @restriction : column constraint with @name must exists in table 
 */
query RDB::operations::RemoveColumnConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isConstraintForColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name);
}

mapping RDB::operations::RemoveColumnConstraint::apply(inout gen : RDB::ModelGeneration) {
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.constraints := c.constraints->excluding(gen.findColumnConstraint(self.owningSchemaName, self.owningTableName, self.owningColumnName, self.name));
}

/**
 * REMOVE INDEX
 * This operation remove index from schema
 * @restriction : index with @name must exists in schema 
 * @restristion : index can't be used by PrimaryKey or UniqueIndex constraints
 */
query RDB::operations::RemoveIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isIndexInSchema(self.owningSchemaName, self.name)
		and not gen.isIndexUsed(self.owningSchemaName, self.name);
}

mapping RDB::operations::RemoveIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes := s.indexes->excluding(gen.findIndex(self.owningSchemaName, self.name));
}

/**
 * REMOVE DEFAULT VALUE
 * This operation remove default value and set value to NULL
 * Column must exists in actual model generation.
 * @restriction :  column with @name must exists in generation
 */
query RDB::operations::RemoveDefaultValue::isValid(gen : RDB::ModelGeneration) : Boolean { 
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);
}

mapping RDB::operations::RemoveDefaultValue::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.defaultValue := null;
}

/********************************************************************************
 *								DATA OPERATIONS									*
 ********************************************************************************/

/**
 * COPY INSTANCES
 * This operation copy data from one column to another. 
 * Target and source column can be in the same table.
 * MergeType:
 * strict -> Can not transfer data if a tables have different number of instances (rows).
 * force -> Delete rows if there is more instancef in source table. If source table has less number
 * of instances add default value or null.
 * @restriction : column with @sourceColumnName must exists in table
 * @restriction : target column with @targetColumnName must exists in table
 * @restriction : target and source column type must be same
 */
query RDB::operations::CopyInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.sourceColumnName)
		   and gen.isColumnInTable(self.owningSchemaName, self.targetTableName, self.targetColumnName)
		   and gen.isSameColumnType(self.owningSchemaName, self.owningTableName, self.sourceColumnName, self.targetTableName, self.targetColumnName);
}

mapping RDB::operations::CopyInstances::apply(inout gen : RDB::ModelGeneration){
}

/**
 * INSERT INSTANCES
 * This operation copy data from collection of columns.
 * @restriction : columns with @sourceColumnNames must exists in source table
 * @restriction : columns with @targetColumnNames must exists in target table
 * @restriction : columns must have same name and column type
 */
query RDB::operations::InsertInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.areColumnsInTable(self.owningSchemaName, self.sourceTableName, self.sourceColumnsNames)
		   and gen.areColumnsInTable(self.owningSchemaName, self.targetTableName, self.targetColumnsNames)
		   and gen.areSameColumnsTypes(self.owningSchemaName, self.sourceTableName, self.sourceColumnsNames, self.targetTableName, self.targetColumnsNames);
}

mapping RDB::operations::InsertInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * GENERATE SEQUENCE NUMBERS
 * This operation generate new sequence numbers to defined columns.
 * @restriction : column with @columnName must exists in defined table
 * @restriction : sequence with @sequenceName must exists in schema
 */
query RDB::operations::GenerateSequenceNumbers::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.tableName, self.columnName)
		   and gen.isSequenceInSchema(self.owningSchemaName,self.sequenceName);
}

mapping RDB::operations::GenerateSequenceNumbers::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * HAS NO OWN INSTANCES
 * This operation check if table has some own rows.
 * @restriction : table with @tableName must exists in generation
 * @restriction : child tables with @descendantNames must exists in generation
 */
query RDB::operations::HasNoOwnInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.tableName)
		   and gen.areTablesInSchema(self.owningSchemaName, self.descendantsNames);
}

mapping RDB::operations::HasNoOwnInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * HAS NO INSTANCES
 * This operation check if table has any rows.
 * @restriction : table with @tableName must exists in generation
 */
query RDB::operations::HasNoInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.tableName);
}

mapping RDB::operations::HasNoInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * ADD INSTANCES
 * This operation add defined number of rows to defined tables.
 * @restriction : table with @sourceTableName must exists in generation
 * @restriction : target tables with @name must exists in generation
 */
query RDB::operations::AddInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.sourceTableName)
		   and gen.areTablesInSchema(self.owningSchemaName, self.targetTableNames);
}

mapping RDB::operations::AddInstances::apply(inout gen : RDB::ModelGeneration){
}


/********************************************************************************
 *									QUERIES										*
 ********************************************************************************/

/********************************** SCHEMA **************************************/
query RDB::ModelGeneration::isSchemaInGeneration(sName : String) : Boolean {
	return self.schemas->exists(name = sName);
}

query RDB::ModelGeneration::findSchema(sName : String) : RDB::Schema {
	return self.schemas->selectOne(name = sName);
}


/********************************** SEQUENCE ************************************/
query RDB::ModelGeneration::isSequenceInSchema(sName : String, qName : String) : Boolean {
	return self.findSchema(sName).sequences->exists(name = qName);
}

query RDB::ModelGeneration::findSequence(sName : String, qName : String) : RDB::Sequence {
	return self.findSchema(sName).sequences->selectOne(name = qName);
}


/************************************ INDEX *************************************/
query RDB::ModelGeneration::isIndexInSchema(sName : String, iName : String) : Boolean {
	return self.findSchema(sName).indexes->exists(name = iName);
}

query RDB::ModelGeneration::findIndex(sName : String, iName : String) : RDB::Index {
	return self.findSchema(sName).indexes->selectOne(name = iName);
}

query RDB::ModelGeneration::isIndexUsed(sName : String, iName : String) : Boolean {
return self.findSchema(sName).tables.constraints->select(c | c.oclIsKindOf(RDB::PrimaryKey)).oclAsType(RDB::PrimaryKey)->exists(underlyingIndex.name = iName)
	or self.findSchema(sName).tables.constraints->select(c | c.oclIsKindOf(RDB::UniqueIndex)).oclAsType(RDB::UniqueIndex)->exists(underlyingIndex.name = iName);
}


/*********************************** TABLE *************************************/
query RDB::ModelGeneration::isTableInSchema(sName : String, tName : String) : Boolean {
	return self.findSchema(sName).tables->exists(name = tName);
}

query RDB::ModelGeneration::areTablesInSchema(sName : String, tNames : OrderedSet(String)) : Boolean {
	return tNames->forAll(name | self.isTableInSchema(sName, name));
}

query RDB::ModelGeneration::findTable(sName : String, tName : String) : RDB::Table {
	return self.findSchema(sName).tables->selectOne(name = tName);
}

query RDB::ModelGeneration::isConstraintForTable(sName : String, tName : String, rName : String) : Boolean {
	return self.findTable(sName, tName).constraints->exists(name = rName);
}


/*********************************** COLUMN ************************************/
query RDB::ModelGeneration::isColumnInTable(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).columns->exists(name = cName);
}

query RDB::ModelGeneration::areColumnsInTable(sName : String, tName : String, cNames : OrderedSet(String)) : Boolean {
	return cNames->forAll(name | self.isColumnInTable(sName, tName, name));
}

query RDB::ModelGeneration::findColumn(sName : String, tName : String, cName : String) : RDB::Column {
	return self.findTable(sName, tName).columns->selectOne(name = cName);
}

query RDB::ModelGeneration::findColumns(sName : String, tName : String, cNames : OrderedSet(String)) : OrderedSet(RDB::Column) {
	var columns : OrderedSet(RDB::Column);
	cNames->forEach(cName) {
		columns += self.findColumn(sName, tName, cName);
	};
	return columns;
}

query RDB::ModelGeneration::isConstraintForColumn(sName : String, tName : String, cName : String, rName : String) : Boolean {
	return self.findColumn(sName, tName, cName).constraints->exists(name = rName);
}



/******************************* TABLE CONSTRAINT *******************************/
query RDB::ModelGeneration::findTableConstraint(sName : String, tName : String, rName : String) : RDB::TableConstraint {
	return self.findTable(sName, tName).constraints->selectOne(name = rName);
}

query RDB::ModelGeneration::isUnique(sName : String, tName : String, cName : String) : Boolean {
return self.findTable(sName, tName).constraints->select(c | c.oclIsKindOf(RDB::UniqueIndex)).oclAsType(RDB::UniqueIndex)->exists(underlyingIndex.columns->exists(name = cName));
}

query RDB::ModelGeneration::isPrimaryKey(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).constraints->select(c | c.oclIsKindOf(RDB::PrimaryKey)).oclAsType(RDB::PrimaryKey)->exists(underlyingIndex.columns->exists(name = cName));

}

query RDB::ModelGeneration::isForeignKey(sName : String, tName : String, cName : String) : Boolean {
	return self.findTable(sName, tName).constraints->select(c | c.oclIsKindOf(RDB::ForeignKey)).oclAsType(RDB::ForeignKey)->exists(constrainedColumn.name = cName);
}


/****************************** COLUMN CONSTRAINT *******************************/
query RDB::ModelGeneration::findColumnConstraint(sName : String, tName : String, cName : String, rName : String) : RDB::ColumnConstraint {
	return self.findColumn(sName, tName, cName).constraints->selectOne(name = rName);
}

query RDB::ModelGeneration::isNotNull(sName : String, tName : String, cName : String) : Boolean {
	return self.findColumn(sName, tName, cName).constraints->size() > 0;
}


/********************************* COLUMN TYPE *********************************/
// overi, zda primitivni typ existuje (true - exituje, false - neexistuje)
query RDB::ModelGeneration::isPrimitiveType(type : String) : Boolean {
	// je treba prohledat enum, zda se v nem nachazi nas hledany prvek
	return not (self.createPrimitiveType(type) = null);
}

// prirazeni primitivni hodnoty podle vstupniho stringu
query RDB::ModelGeneration::createPrimitiveType (type : String) : RDB::PrimitiveType {
	switch {
		case (type = "boolean") return RDB::PrimitiveType::boolean;
		case (type = "char") return RDB::PrimitiveType::char;
		case (type = "int") return RDB::PrimitiveType::int;
		case (type = "float") return RDB::PrimitiveType::float;
	};
	return null;
}

query RDB::ModelGeneration::isSameColumnName(sName : String, tNameA : String, cNameA : String, tNameB : String, cNameB : String) : Boolean {
	return self.findColumn(sName, tNameA, cNameA).name = self.findColumn(sName, tNameB, cNameB).name;
}

query RDB::ModelGeneration::isSameColumnType (sName : String, tNameA : String, cNameA : String, tNameB : String, cNameB : String) : Boolean {
	return self.findColumn(sName, tNameA, cNameA).type = self.findColumn(sName, tNameB, cNameB).type;
}

query RDB::ModelGeneration::areSameColumnsTypes (sName : String, tNameA : String, cNameA : OrderedSet(String), tNameB : String, cNameB : OrderedSet(String)) : Boolean {
	var b : Boolean := true;
	cNameA->forEach(nameA){
		cNameB->forEach(nameB){
			if(self.isSameColumnName(sName, tNameA, nameA, tNameB, nameB) 
				and not self.isSameColumnType(sName, tNameA, nameA, tNameB, nameB)) then {
				b := false;
			} endif;
		};
	};
	return b;
}