/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

import queries_app;
	
modeltype APP uses 'http://www.collectionspro.eu/jam/mm/app';
transformation APP_VALIDATE(in inModel : APP);

main() {	
	log("Validation aplication model started...");
	if(inModel.rootObjects()![APP::ModelRoot].isModelValid())then{
		log("Model valid");
	}endif;
	log("");
}
///////////////---------------------------------Validacni query --------------------------------///////////
/*
	Validacni query app modelu
*/
query app::ModelRoot::testID() : Boolean {
  var classes : Sequence(app::Class) :=  self.modelGenerations->classes[app::Class];
  return not classes->forAll(cls|cls.assertIDCondition());
}

/** 
	Vraci false, pokud model neobsahuje cykly hierarchii dedicnosti, jinak true

query app::ModelGeneration::containsCycle():Boolean{
		return not self.classes[app::Class]->forAll(cls | not cls.containsCycle(OrderedSet{}));
};
*/
query app::ModelRoot::isModelValid():Boolean{
	var validity : Boolean := true;
	var classUniqueName : Boolean := true; 
	var propUniqueName : Boolean := true;
	var notCycledHierarchies : Boolean := true;
	var oppositeProperty := true;
	var embeddedBounds := true;
	//classes leading to cycle
	var cycles : Set(Class) := Set{};
	self.modelGenerations->classes->forEach(cls | not cls.hasUniqueName()) {
		classUniqueName := false;
		log("ambigous Class name " + cls.name);		
	};
	self.modelGenerations->classes[app::Class]->properties->forEach(prop | not prop.hasUniqueNameInHierarchy()) {
		log("ambigous property name " + prop.name + " in class " + prop.owningClass.name);
		propUniqueName := false;		
	};
	self.modelGenerations ->classes[Class]->forEach(cls | cls.containsCycle(OrderedSet{}) and not cycles->includes(cls)) {
		var  iterateClass : Class := cls;  
		var output : String := "Generation " + self.modelGenerations->indexOf(cls.owningModel).repr() + "contains cycled hierarchy:" + cls.name;
		while(not cycles->includes(iterateClass)){
			cycles += iterateClass;
			iterateClass := iterateClass.parent;
			output := output + ", " + iterateClass.name;					
		};
		log(output);
		notCycledHierarchies := false;
	};
	self.modelGenerations->classes[app::Class]->properties->forEach(prop | not prop.isOppositeCorrectlySet()){
		oppositeProperty := false;
		log("Generation " + self.modelGenerations->indexOf(prop.owningClass.owningModel).repr() + " containst property " + prop.name+ " property of " + prop.owningClass.name + " not correctly set");
	};
	self.modelGenerations->classes[app::Class]->properties->forEach(prop | not prop.areEmbeddedArritiesCorrectlySet()){
		log("Generation " + self.modelGenerations->indexOf(prop.owningClass.owningModel).repr() + " containst embedded property " + prop.name + " of type " + prop.type.name + " with bounds not correctly set");
		embeddedBounds :=false;
	};
	if(self.modelGenerations->exists(areEmbeddedPropertiesInNameCollision()))then{
		log("Model invalid - Embedded property in name collision");
		validity := false;
	}endif;
	if(self.testID())then{
		log("Model invalid - ambiguous IDs in classes");
		validity := false;
	}endif;
	if(not self.modelGenerations->forAll(assertTPCCondition()))then{
		log("Model invalid - contains TPC expansion");
		validity := false;
	}endif;
	
	
	if(not classUniqueName)then{
		log("Model invalid - ambigous class names in generation");
	}endif;
	if(not propUniqueName)then{
		log("Model invalid - ambigous property names in class hierarchy");
		}endif;
	if(not oppositeProperty)then{
		log("Model invalid - properties and oppositeProperties are not correctly set");
	}endif;
	if(not embeddedBounds)then{
		log("Model invalid - Embedded property bounds are not correctly set");
	}endif;	
	return  ;
}

query app::ModelGeneration::assertTPCCondition():Boolean{
	var validity : Boolean := true;
	
	self.classes[app::Class]->properties->forEach(prop | (prop.owningClass.isNonleafTPCClass() and
	  (prop.upperBound > 1 or prop.upperBound = -1))){
		log(self.modelRoot.modelGenerations->indexOf(self).repr() + ". generation contains TPC hierarchy causing table expansion "+
		" class " + prop.owningClass.name + " property:" + prop.repr());
		validity := false;
	};
	return validity;
}

/*
	Overuje, jestli dana Property reprezentuje unidirectional (jednostranne navigabilni) vazbu 1-1,
	ktera v nynejsi verzi neni vytvoritelna
*/
query app::Property::isOneToOneUnidirectional():Boolean{
	return false;
}


query app::ModelGeneration::containsNonleafTPCAssociation():Boolean{
	return self.classes[app::Class]->properties->exists( prop | not prop.isPrimitive() and prop.owningClass.isNonleafTPCClass() and 
	(prop.isOneToOneUnidirectional()or prop.isOneToOneBidirectional() or prop.isOneToManyUnidirectional() or 
	prop.isOneToManyBidirectional() or prop.isManyToManyUnidirectional() or prop.isManyToManyBidirectional()));
}


query app::Class::assertIDCondition() : Boolean{
    if(self.hasNonRootId())then{
    	log("Non-root class " + self.name + " contains id property.");
    	return false;
    }endif;

	if(not self.areIDBoundsCorrectlySet())then{
		log("Id bounds in class " + self.name + " not correctly set");
		return false;
	}endif;
	if(not self.areIDTypesCorrectlySet())then{
	    log("Id type in class " + self.name + " is not primitive Integer");
		return false;
	}endif;
	return true;
}



query app::Class::hasNonRootId() : Boolean{
	if(self.parent <> null and self.properties->select(prop | prop.isID)->size() != 0)then{
		return true;
	}endif;
	return false;
}

query app::Class::areIDTypesCorrectlySet() : Boolean{
    return self.properties->forAll(pr | not pr.isID or (pr.type.name = 'Integer' and pr.type.oclIsKindOf(app::PrimitiveClass)));
}

query app::Class::areIDBoundsCorrectlySet() : Boolean{
    return self.properties->forAll(pr | not pr.isID or (pr.lowerBound = 1 and pr.upperBound = 1));
}


query app::Property::areEmbeddedArritiesCorrectlySet():Boolean{
	return self.type.oclIsKindOf(app::EmbeddedClass) and (self.upperBound <> 1 or 
	not(self.lowerBound = 1 or self.lowerBound = 0));		
}

query app::ModelGeneration::areEmbeddedPropertiesInNameCollision():Boolean{
	var validity : Boolean := false;
	self.classes[app::EmbeddedClass]->properties ->forEach(prop | prop.isEmbeddedInNameCollission()){
		log("property " + prop.name + " name is in collision with owningClass property");
		validity := true;
	};
	return validity;
}

query app::Property::isEmbeddedInNameCollission() :Boolean{
	return self.owningClass->owningModel->classes[app::Class]->select(cls | cls.properties->exists(prop | self.owningClass = prop.type))->
	properties->exists(prop | prop.name = self.type.name + "_" + self.name);
}

query app::Property::isOppositeCorrectlySet():Boolean{
	if( self.oppositeProperty = null or self.oppositeProperty.oppositeProperty = self )then{
		return true;
	}endif;
	return false;
}

query app::Entity::hasUniqueName():Boolean{
	var validity : Boolean := true;
	self.owningModel->classes->forEach(cls | cls.name = self.name and cls <> self){
		validity := false;
	};
	return validity;
}


query app::Property::hasUniqueNameInHierarchy():Boolean{
    if(not self.owningClass.oclIsTypeOf(Class))then{
    	return false;
    }endif;
	self.owningClass[Class].getPathToRoot()->properties->forEach(prop | prop.name = self.name and prop <> self){
		return false;
	};
	return true;	
}