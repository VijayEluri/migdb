/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */

modeltype APP uses 'mm-app';

transformation APP_To_Rdb(in inModel : APP, out outModel : APP);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(APP::reduced::ModelRoot)));
	
	log("transformation started...");
	
	inModel.rootObjects().deepclone();

	var root : APP::reduced::ModelRoot = outModel.rootObjects()![APP::reduced::ModelRoot];
	var gen0 : APP::reduced::ModelGeneration = root.modelGenerations->first();
	
	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
	
	root.operations = root.operations->reject(true);
}

// kontrola validity operace
abstract query APP::reduced::operations::ModelOperation::
	isValid(gen : APP::reduced::ModelGeneration) : Boolean;

// namapování dané operace
abstract mapping APP::reduced::operations::ModelOperation::
	apply(inout gen : APP::reduced::ModelGeneration);


/* ++++++++++ ADD OPERATIONS ++++++++++++++++++++ */

query APP::reduced::operations::AddClass::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return not gen.isAbstractClassInGeneration(self.name)
		   and gen.isAbstractClassInGeneration(self.parent)
		   and not (self.name = self.parent);
}

// pøidání standardní tøídy do generace
mapping APP::reduced::operations::AddClass::apply(inout gen : APP::reduced::ModelGeneration) {
	gen.classes += object APP::reduced::Class {
		name := self.name;
		parent := gen.findClass(self.parent);
		isAbstract := self.isAbstract;
		isEmbedded := self.isEmbedded;
		tableName := self.tableName;
		inheritanceType := self.inheritanceType;
	};
	
	// vytvoøení id atributu
	if (self.parent = null) then {
		var c : APP::reduced::Class := gen.findClass(self.name);
		c.properties += object APP::reduced::Property {
			name := "id";
			isID := true;
			// [?] nutné nastavit typ atributu
		};
	} endif;
}

query APP::reduced::operations::AddPrimitiveClass::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return not gen.isAbstractClassInGeneration(self.name);
}

// pøidá primitivní tøídu do generace
mapping APP::reduced::operations::AddPrimitiveClass::apply(inout gen : APP::reduced::ModelGeneration) {
	gen.classes += object APP::reduced::PrimitiveClass {
		name := self.name;
		primitiveType := self.primitiveType;
	};
}

query APP::reduced::operations::AddProperty::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and not gen.isPropertyInClass(self.name, self.owningClassName)
	// ovìøí, zda-li existuje tøída požadovaného typu atributu
		   and gen.isAbstractClassInGeneration(self.type)
	// ovìøí, zda-li jsou hranice nastaveny smysluplnì
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
	// v hierarchii neexistuje atribut se stejným názvem
		   and not gen.isPropertyInHierarchy(self.owningClassName, self.name);
}

// Pri vytvoreni Property lze vytvorit jednostranne navigabilni asociaci
// lze nastavit i kolekci:
//isOrdered := self.isOrdered; // has to be a collection (upperBound == -1)
//isUnique := self.isUnique; // has to be a collection (upperBound == -1)
mapping APP::reduced::operations::AddProperty::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	
	c.properties += object APP::reduced::Property {
		name := self.name;
		type := gen.findClass(self.type);
		defaultValue := self.defaultValue; // a value type has to be equal with a type of property
		lowerBound := self.lowerBound;
		upperBound := self.upperBound;
		isOrdered := self.isOrdered; // has to be a collection (upperBound == -1)
		isUnique := self.isUnique; // has to be a collection (upperBound == -1)
		columnName := self.columnName;
		columnNamePattern := self.columnNamePattern;
		sequenceName := self.sequenceName;
	};
}

query APP::reduced::operations::SetOpposite::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	var pre : Boolean := 
	// tøídy existují v dané generaci
		gen.isAbstractClassInGeneration(self.firstClassName)
		and gen.isAbstractClassInGeneration(self.secondClassName)
    // atributy existují v daných tøídách
		and gen.isPropertyInClass(self.firstPropertyName, self.firstClassName)
		and gen.isPropertyInClass(self.secondPropertyName, self.secondClassName);
	if (self.isOpposite = true) then {
		return pre
		// nejedná se o oboustrannì navigabilní asociaci
			and gen.findClass(self.firstClassName).findProperty(self.firstPropertyName).oppositeProperty = null
			and gen.findClass(self.secondClassName).findProperty(self.secondPropertyName).oppositeProperty = null;
	} else {
		return pre
		// jedná se o oboustrannì navigabilní asociaci
			and gen.findClass(self.firstClassName).findProperty(self.firstPropertyName).oppositeProperty.name = self.secondPropertyName
			and gen.findClass(self.secondClassName).findProperty(self.secondPropertyName).oppositeProperty.name = self.firstPropertyName;
	} endif;
	
	// nedosažitelný stav
	return null;
}

mapping APP::reduced::operations::SetOpposite::apply(inout gen : APP::reduced::ModelGeneration) {
	var p1 : APP::reduced::Property := gen.findClass(self.firstClassName).findProperty(self.firstPropertyName);
	var p2 : APP::reduced::Property := gen.findClass(self.secondClassName).findProperty(self.secondPropertyName);
	if (self.isOpposite = true) then {
		p1.oppositeProperty := p2;
		p2.oppositeProperty := p1;
	} else {
		p1.oppositeProperty := null;
		p2.oppositeProperty := null;
	} endif;	
}


/* ++++++++++ SET OPERATIONS FOR CLASSES ++++++++++++++++++++ */

query APP::reduced::operations::SetAbstract::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name);
}

mapping APP::reduced::operations::SetAbstract::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.name);
	c.isAbstract := self.isAbstract;
}

query APP::reduced::operations::SetEmbedded::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name);
}

mapping APP::reduced::operations::SetEmbedded::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.name);
	c.isEmbedded := self.isEmbedded;
}

query APP::reduced::operations::SetParent::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name)
		   and gen.isAbstractClassInGeneration(self.parentName)
	       and not gen.isSelfParent(self.parentName, self.name, true);
}

mapping APP::reduced::operations::SetParent::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.name);
	c.parent := gen.findClass(self.parentName);	
}

/* ++++++++++ SET OPERATIONS FOR PROPERTIES ++++++++++++++++++++ */

query APP::reduced::operations::SetType::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInClass(self.name, self.owningClassName)
		   and gen.isAbstractClassInGeneration(self.type);
}

mapping APP::reduced::operations::SetType::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	var p : APP::reduced::Property := c.findProperty(self.name);
	p.type := gen.findClass(self.type); // null
}

query APP::reduced::operations::SetBoundries::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInClass(self.name, self.owningClassName)
		   // ovìøí, zda-li jsou hranice nastaveny smysluplnì
		   and (self.lowerBound <= self.upperBound or (self.upperBound = -1 and self.lowerBound > self.upperBound))
}

// nastaví hranice atributu
mapping APP::reduced::operations::SetBoundries::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	var p : APP::reduced::Property := c.findProperty(self.name);
	// nastaví dolní hranici atributu
	p.lowerBound := self.lowerBound;
	// nastaví horní hranici atributu
	p.upperBound := self.upperBound;
}

query APP::reduced::operations::SetOrdered::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInClass(self.name, self.owningClassName);
}

mapping APP::reduced::operations::SetOrdered::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	var p : APP::reduced::Property := c.findProperty(self.name);
	p.isOrdered := self.isOrdered;
}

query APP::reduced::operations::SetUnique::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInClass(self.name, self.owningClassName);
}

mapping APP::reduced::operations::SetUnique::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	var p : APP::reduced::Property := c.findProperty(self.name);
	p.isUnique := self.isUnique;
}


/* ++++++++++ EDIT OPERATIONS ++++++++++++++++++++ */

query APP::reduced::operations::RenameClass::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.name)
		   and not gen.isAbstractClassInGeneration(self.newName);
}

mapping APP::reduced::operations::RenameClass::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.name);
	// pøejmenování tøídy
	c.name := self.newName;
	// [!] refactoring ukazatelù na danou tøídu s novým názvem není nutný
}

query APP::reduced::operations::RenameProperty::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	return gen.isAbstractClassInGeneration(self.owningClassName)
		   and gen.isPropertyInClass(self.name, self.owningClassName)
		   and not gen.isPropertyInClass(self.newName, self.owningClassName);
}

mapping APP::reduced::operations::RenameProperty::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	var p : APP::reduced::Property := c.findProperty(self.name);
	p.name := self.newName;
}

/* ++++++++++ REMOVE OPERATIONS ++++++++++++++++++++ */

query APP::reduced::operations::RemoveClass::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
	// tøída existuje v dané generaci
	return gen.isAbstractClassInGeneration(self.name)
	// tøídá neobsahuje žádné atributy
	       and (gen.findClass(self.name).properties->isEmpty()
	// nebo obsahuje právì jeden s vlastností 'id'
	       		or gen.findClass(self.name).properties->forAll(isID = true))
	// na tøídu se neodkazuje žádná jiná tøída
		   and not gen.classes.properties->exists(type = self)
	// tøída není v hierarchii
		   and not gen.classes->exists(parent.name = self.name);
}

// odstraní tøídu z dané generace
mapping APP::reduced::operations::RemoveClass::apply(inout gen : APP::reduced::ModelGeneration) {
	// odstranìní tøídy
	gen.classes := gen.classes->excluding(gen.findClass(self.name));
}

query APP::reduced::operations::RemoveProperty::isValid(gen : APP::reduced::ModelGeneration) : Boolean {
    // atribut existuje v dané tøídì
	return gen.isPropertyInClass(self.name, self.owningClassName)
	// atribut nemá vlastnost 'id'
		   and gen.findClass(self.owningClassName).findProperty(self.name).isID = false
	// neexistuje asociace z jiné tøídy
		   and gen.findClass(self.owningClassName).findProperty(self.name).oppositeProperty = null;
}

// odstraní atribut z dané tøídy
mapping APP::reduced::operations::RemoveProperty::apply(inout gen : APP::reduced::ModelGeneration) {
	var c : APP::reduced::Class := gen.findClass(self.owningClassName);
	// odstranìní atributu
	c.properties := c.properties->excluding(c.findProperty(self.name));
}

/* ++++++++++ COMPLEX OPERATIONS ++++++++++++++++++++ */

query APP::reduced::operations::CopyProperty::isValid(gen : APP::reduced::ModelGeneration) : Boolean{
	return gen.classes->exists(name = self.owningClassName)
		   and gen.classes->exists(name = self.targetClassName)
		   and gen.findClass(self.owningClassName).properties->exists(name = self.propertyName)
		   and self.endInOwerload != null // testujeme, zda je nejak nastaveno chovani pri preteceni
		   and self.addValueNull != null // testujeme zda je nejak nastaveno chovani pri mensim poctu instanci
		   // musime jeste otestovat, zda jsou obe tridy v hierarchii
		   // OR pouzima proto, ze nejprve testuji zda je target class parentem a jednou childem (je jedno jak hluboko v hierarchii)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName) or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}

/* Operace ma jeste dve property:
	 andInOwerload (true - zkonceni pri preteceni)
	 addValueNull (true - pokud bude mene instanci nez v cilove tride, doplnime hodnoty na NULL; false - hodnoty se dopni na default)
	 
	 WARNING: property jeste nejsou nijak implementovane
*/
mapping APP::reduced::operations::CopyProperty::apply(inout gen : APP::reduced::ModelGeneration){
	var c : APP::reduced::Class := gen.findClass(self.targetClassName);
	var p : APP::reduced::Property := gen.findClass(self.owningClassName).findProperty(self.propertyName);
	
	c.properties += object APP::reduced::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
		columnName := p.columnName;
		columnNamePattern := p.columnNamePattern;
		sequenceName := p.sequenceName;
	};
}

query APP::reduced::operations::MoveProperty::isValid(gen : APP::reduced::ModelGeneration) : Boolean{
	return gen.classes->exists(name = self.owningClassName)
		   and gen.classes->exists(name = self.targetClassName)
		   and gen.findClass(self.owningClassName).properties->exists(name = self.propertyName)
		   and self.endInOwerload != null // testujeme, zda je nejak nastaveno chovani pri preteceni
		   and self.addValueNull != null // testujeme zda je nejak nastaveno chovani pri mensim poctu instanci
		   // musime jeste otestovat, zda jsou obe tridy v hierarchii
		   // OR pouzima proto, ze nejprve testuji zda je target class parentem a jednou childem (je jedno jak hluboko v hierarchii)
		   and (gen.isInHierarchy(self.owningClassName, self.targetClassName) or gen.isInHierarchy(self.targetClassName, self.owningClassName));
}

mapping APP::reduced::operations::MoveProperty::apply(inout gen : APP::reduced::ModelGeneration){
	var c : APP::reduced::Class := gen.findClass(self.targetClassName);
	var p : APP::reduced::Property := gen.findClass(self.owningClassName).findProperty(self.propertyName);
	
	c.properties += object APP::reduced::Property {
		name := p.name;
		type := p.type;
		defaultValue := p.defaultValue;
		lowerBound := p.lowerBound;
		upperBound := p.upperBound;
		isOrdered := p.isOrdered;
		isUnique := p.isUnique;
		columnName := p.columnName;
		columnNamePattern := p.columnNamePattern;
		sequenceName := p.sequenceName;
	};
	
	// smazeme property ze tridy c
	c.properties := c.properties->excluding(c.findProperty(self.propertyName));
	
}

/* ++++++++++ QUERIES FOR OPERATIONS ++++++++++++++++++++ */

// vrátí tøídu daného jména
query APP::reduced::ModelGeneration::findClass(cName : String) : APP::reduced::Class {	 
	 return self.classes->oclAsType(APP::reduced::Class)->selectOne(name = cName);
}

// ovìøí, zda-li v generaci existuje tøída s daným jménem
query APP::reduced::ModelGeneration::isAbstractClassInGeneration(cName : String) : Boolean {
	return self.classes->exists(name = cName);
}

// vrátí atribut daného jména
query APP::reduced::Class::findProperty(pName : String) : APP::reduced::Property {
	return self.properties->selectOne(name = pName);
}

// ovìøí, zda-li ve tøídì existuje atribut daného jména
query APP::reduced::Class::isPropertyInClass(pName : String) : Boolean {
	return self.properties->exists(name = pName);
}

// ovìøí, zda-li je tøída sama sobì rodièem
query APP::reduced::ModelGeneration::isSelfParent(cName : String, startName : String, entry : Boolean) : Boolean {
	var c : APP::reduced::Class := self.findClass(cName);
	if (c <> null) then {
		if (not entry and cName = startName) then { return true; } endif;
		if (c.parent <> null) then { return self.isSelfParent(c.parent.name, startName, false); } endif;
	} endif;
	return false;
}

// ovìøí, zda-li je sClass rodièem fClass
query APP::reduced::ModelGeneration::isInHierarchy(fClass : String, sClass : String) : Boolean {
	var ourClass : APP::reduced::Class := self.findClass(fClass);
	if(ourClass <> null) then {
		if (ourClass.parent <> null and ourClass.parent = sClass) then { return true; } endif;
		if (ourClass.parent <> null) then {return self.isInHierarchy(ourClass.parent.name, sClass); } endif;
	}endif;
	return false;
}

// ovìøí, zda-li v hierarchii existuje atribut se stejným názvem
query APP::reduced::ModelGeneration::isPropertyInHierarchy(cName : String, pName : String) : Boolean {
	// pro každý atribut se stejným názvem...
	self.classes.properties->forEach(p | p.name = pName) {
		// ...ovìøíme, zda-li se tøídy, kterým tyto atributy náleží, nachází ve stejné hierarchii
		if (self.isInHierarchy(cName, p.owningClass.name) = true or self.isInHierarchy(p.owningClass.name, cName) = true) then {
			return true;
		} endif;
	};
	return false;
}
