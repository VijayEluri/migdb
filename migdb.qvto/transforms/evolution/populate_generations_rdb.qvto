import queries_rdb_link;
import builder_rdb_link; 

modeltype RDB uses rdb('http://www.collectionspro.eu/jam/mm/rdb');

transformation RDBEVO(in inModel : RDB, out outModel : RDB);
main() {
	assert(inModel.rootObjects()->size() = 1);
	assert(inModel.rootObjects()->forAll(oclIsKindOf(RDB::ModelRoot)));

	log("RDB evolution started...");

	inModel.rootObjects().deepclone();

	var root : RDB::ModelRoot = outModel.rootObjects()![RDB::ModelRoot];
	var gen0 : RDB::ModelGeneration = root.modelGenerations->first();

	root.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(gen0)) then {
			log("	operation is valid " + op.repr());
			op.xmap apply(gen0);
		} else {
			log("	operation is invalid " + op.repr());
			break;
		} endif;
	};
}

abstract query RDB::ops::ModelOperation::
	isValid(gen : RDB::ModelGeneration) : Boolean;

abstract mapping RDB::ops::ModelOperation::
	apply(inout gen : RDB::ModelGeneration);


/********************************************************************************
 *								ADD OPERATIONS									*
 ********************************************************************************/


/**
 * ADD SCHEMA
 * Operation create new database schema in generation model
 * @restriction : schema with @name must not exists in generation
 */
query RDB::ops::AddSchema::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isSchemaInGeneration(self.name);
}

mapping RDB::ops::AddSchema::apply(inout gen : RDB::ModelGeneration) {
	gen.schemas += _schema(self.name);
}

/**
 * ADD SEQUENCE
 * we can use only one global sequence in the system. 
 * Operation is something like singleton - Sequence can be created only one times.
 * @restriction : sequence with @newDefaultValue must not exists in generation
 * @restriction : in the schema must not be other sequence
 */
query RDB::ops::AddSequence::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isSequenceInSchema(self.owningSchemaName, self.name);
}

mapping RDB::ops::AddSequence::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.sequence := _sequence(self.name, self.startValue); 
}

/**
 * ADD INDEX
 * Create index in defined schema.
 * @restriction : in the schema must not be an index with the same @name.
 * @restriction : columns with @columnsNames must exists in table
 */
query RDB::ops::AddIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isIndexInSchema(self.owningSchemaName, self.name)
		and gen.areColumnsInTable(self.owningSchemaName, self.owningTableName, self.columnsNames);
}

mapping RDB::ops::AddIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes += _index(self.name, gen.findColumns(self.owningSchemaName, self.owningTableName, self.columnsNames)); 
}

/**
 * ADD TABLE
 * Create table in defined schema.
 * @restriction : in the schema must not be a table with the same @name.
 */
query RDB::ops::AddTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isTableInSchema(self.owningSchemaName, self.name);
}

mapping RDB::ops::AddTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.tables += _table(self.name);
}

/**
 * ADD COLUMN
 * Create column in defined table.
 * @restriction : in the table must not be a column with the same @name.
 * @restriction : @type must exist in PrimitiveType.
 */
query RDB::ops::AddColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isPrimitiveType(self.type);
}

mapping RDB::ops::AddColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.columns += _column(self.name, gen.createPrimitiveType(self.type), self.defaultValue); 
}

/**
 * ADD UNIQUE
 * Create unique constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : column with @columnName must exists in table
 */
query RDB::ops::AddUnique::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.constrainedColumnName);
}

mapping RDB::ops::AddUnique::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += _unique(self.name, gen.findColumn(self.owningSchemaName, self.owningTableName, self.constrainedColumnName));
}

/**
 * ADD PRIMARY KEY
 * Create primary key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : index with @underlyingIndexName must exists in schema
 * @restriction : column with @columnName must exists in table
 */
query RDB::ops::AddPrimaryKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.constrainedColumnName);
}

mapping RDB::ops::AddPrimaryKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += _primaryKey(self.name, gen.findColumn(self.owningSchemaName, self.owningTableName, self.constrainedColumnName));
}

/**
 * ADD FOREIGN KEY
 * Create foreign key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : column with @constrainedColumnName must exists in table
 * @restriction : table with @targetTableName must exists in schema.
 */
query RDB::ops::AddForeignKey::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
	and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.constrainedColumnName)
	and gen.isTableInSchema(self.owningSchemaName, self.targetTableName);
}

mapping RDB::ops::AddForeignKey::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += _foreignKey(
						self.name, 
						gen.findTable(self.owningSchemaName, self.targetTableName),
						gen.findColumn(self.owningSchemaName, self.owningTableName, self.constrainedColumnName)); 
}

/**
 * ADD NOT NULL
 * Create foreign key constraint on defined column.
 * @restriction : in the table must not be a constraint with the same @name.
 * @restriction : column with @owningColumnName must exists in table
 */
query RDB::ops::AddNotNull::isValid(gen : RDB::ModelGeneration) : Boolean {
	return not gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name)
		and gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.constrainedColumnName);	   
}

mapping RDB::ops::AddNotNull::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints += _notNull(self.name, gen.findColumn(self.owningSchemaName, self.owningTableName, self.constrainedColumnName)); 
}

/********************************************************************************
 *								SET OPERATIONS									*
 ********************************************************************************/

/**
 * RENAME TABLE
 * Create new name for table.
 * @restriction : the table must exists in schema
 * @restriction : in the schema must not be a table with the same @newName.
 */
query RDB::ops::RenameTable::isValid( gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.name)
		and not gen.isTableInSchema(self.owningSchemaName, self.newName);
}

mapping RDB::ops::RenameTable::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.name);
	t.name := self.newName;
}

/**
 * RENAME COLUMN
 * Create new name for column.
 * @restriction : the column must exists in table
 * @restriction : in the table must not be a column with the same @newName.
 */
query RDB::ops::RenameColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.newName);
}

mapping RDB::ops::RenameColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	t.constraints->forEach(r) {
		if (r.oclAsType(RDB::ForeignKey).constrainedColumn.name = c.name.toLower()) then {
			r.oclAsType(RDB::ForeignKey).name := r.oclAsType(RDB::ForeignKey).name.replace("_" + self.name + "_", "_" + self.newName + "_");
		} endif;
	};
	c.name := self.newName;
}

/**
 * SET COLUMN TYPE
 * This operation replace old type of column with a new type of column
 * Column and new type must exists in actual model generation. 
 * @restriction : column with @name must exists in table
 */
query RDB::ops::SetColumnType::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName)
		and gen.isPrimitiveType(self.newType);
}

mapping RDB::ops::SetColumnType::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.type := gen.createPrimitiveType(self.newType);
}

/**
 * SET COLUMN DEFAULT VALUE
 * This operation set new default value to column
 * Column must exists in actual model generation. 
 * If is property isSequence set on TRUE - Sequence must exist in generation - this information is used only in SQL generator
 * In sequence case @newDefaultValue means name of sequence in model generation
 * @restriction : column with @owningColumnName must exists in table
 */
query RDB::ops::SetDefaultValue::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);
}

mapping RDB::ops::SetDefaultValue::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.defaultValue := self.newDefaultValue;
}



/********************************************************************************
 *							 REMOVE OPERATIONS									*
 ********************************************************************************/

/**
 * REMOVE TABLE
 * This operation remove table from schema and generation.
 * @restriction : table with @name must exists in generation
 * @restriction : table must not have columns
 * @restriction : table must not have table constraints
 */
query RDB::ops::RemoveTable::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.name)
		   and gen.findTable(self.owningSchemaName, self.name).columns->size() = 0
		   and gen.findTable(self.owningSchemaName, self.name).constraints->size() = 0;
}

mapping RDB::ops::RemoveTable::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.tables := s.tables->excluding(gen.findTable(self.owningSchemaName, self.name));
}

/**
 * REMOVE COLUMN
 * This operation remove column from table and generation.
 * ForeignKey constraint will be remove with column. 
 * @restriction : column with @name must exists in table
 * @restriction : column mustn't be part of constraint
 */
query RDB::ops::RemoveColumn::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.name)
		and not gen.isConstrainedColumn(self.owningSchemaName, self.owningTableName, self.name);
}

mapping RDB::ops::RemoveColumn::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.name);
	//t.constraints->forEach(r) {
	//	if (r.oclAsType(RDB::ForeignKey).owningColumn.name = c.name.toLower()) then {
	//		t.constraints := t.constraints->excluding(r);
	//	} endif;
	//};
	t.columns := t.columns->excluding(c);
}

/**
 * REMOVE CONSTRAINT
 * This operation remove table constraint from table
 * @restriction : table constraint with @name must exists in table 
 */
query RDB::ops::RemoveConstraint::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isConstraintForTable(self.owningSchemaName, self.owningTableName, self.name);
}

mapping RDB::ops::RemoveConstraint::apply(inout gen : RDB::ModelGeneration) {
	var t : RDB::Table := gen.findTable(self.owningSchemaName, self.owningTableName);
	t.constraints := t.constraints->excluding(gen.findConstraint(self.owningSchemaName, self.owningTableName, self.name));
}


/**
 * REMOVE INDEX
 * This operation remove index from schema
 * @restriction : index with @name must exists in schema 
 */
query RDB::ops::RemoveIndex::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isIndexInSchema(self.owningSchemaName, self.name);
}

mapping RDB::ops::RemoveIndex::apply(inout gen : RDB::ModelGeneration) {
	var s : RDB::Schema := gen.findSchema(self.owningSchemaName);
	s.indexes := s.indexes->excluding(gen.findIndex(self.owningSchemaName, self.name));
}

/**
 * REMOVE DEFAULT VALUE
 * This operation remove default value and set value to NULL
 * Column must exists in actual model generation.
 * @restriction :  column with @name must exists in generation
 */
query RDB::ops::RemoveDefaultValue::isValid(gen : RDB::ModelGeneration) : Boolean { 
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.owningColumnName);
}

mapping RDB::ops::RemoveDefaultValue::apply(inout gen : RDB::ModelGeneration){
	var c : RDB::Column := gen.findColumn(self.owningSchemaName, self.owningTableName, self.owningColumnName);
	c.defaultValue := null;
}

/********************************************************************************
 *								DATA OPERATIONS									*
 ********************************************************************************/

/**
 * COPY INSTANCES
 * This operation copy data from one column to another. 
 * Target and source column can be in the same table.
 * MergeType:
 * strict -> Can not transfer data if a tables have different number of instances (rows).
 * force -> Delete rows if there is more instancef in source table. If source table has less number
 * of instances add default value or null.
 * @restriction : column with @sourceColumnName must exists in table
 * @restriction : target column with @targetColumnName must exists in table
 * @restriction : target and source column type must be same
 */
query RDB::ops::CopyInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.owningTableName, self.sourceColumnName)
		   and gen.isColumnInTable(self.owningSchemaName, self.targetTableName, self.targetColumnName)
		   and gen.isSameColumnType(self.owningSchemaName, self.owningTableName, self.sourceColumnName, self.targetTableName, self.targetColumnName);
}

mapping RDB::ops::CopyInstances::apply(inout gen : RDB::ModelGeneration){
}

/**
 * INSERT INSTANCES
 * This operation copy data from collection of columns.
 * @restriction : columns with @sourceColumnNames must exists in source table
 * @restriction : columns with @targetColumnNames must exists in target table
 * @restriction : columns must have same name and column type
 */
query RDB::ops::InsertInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.areColumnsInTable(self.owningSchemaName, self.sourceTableName, self.sourceColumnsNames)
		   and gen.areColumnsInTable(self.owningSchemaName, self.targetTableName, self.targetColumnsNames)
		   and gen.areSameColumnsTypes(self.owningSchemaName, self.sourceTableName, self.sourceColumnsNames, self.targetTableName, self.targetColumnsNames);
}

mapping RDB::ops::InsertInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * GENERATE SEQUENCE NUMBERS
 * This operation generate new sequence numbers to defined columns.
 * @restriction : column with @columnName must exists in defined table
 * @restriction : sequence with @sequenceName must exists in schema
 */
query RDB::ops::GenerateSequenceNumbers::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isColumnInTable(self.owningSchemaName, self.tableName, self.columnName)
		   and gen.isSequenceInSchema(self.owningSchemaName,self.sequenceName);
}

mapping RDB::ops::GenerateSequenceNumbers::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * HAS NO OWN INSTANCES
 * This operation check if table has some own rows.
 * @restriction : table with @tableName must exists in generation
 * @restriction : child tables with @descendantNames must exists in generation
 */
query RDB::ops::HasNoOwnInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.tableName)
		   and gen.areTablesInSchema(self.owningSchemaName, self.descendantsNames);
}

mapping RDB::ops::HasNoOwnInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * HAS NO INSTANCES
 * This operation check if table has any rows.
 * @restriction : table with @tableName must exists in generation
 */
query RDB::ops::HasNoInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.tableName);
}

mapping RDB::ops::HasNoInstances::apply(inout gen : RDB::ModelGeneration) {

}

/**
 * ADD INSTANCES
 * This operation add defined number of rows to defined tables.
 * @restriction : table with @sourceTableName must exists in generation
 * @restriction : target tables with @name must exists in generation
 */
query RDB::ops::AddInstances::isValid(gen : RDB::ModelGeneration) : Boolean {
	return gen.isTableInSchema(self.owningSchemaName, self.sourceTableName)
		   and gen.areTablesInSchema(self.owningSchemaName, self.targetTableNames);
}

mapping RDB::ops::AddInstances::apply(inout gen : RDB::ModelGeneration){
}