grammar migdb.dsl.ops.Ops with org.eclipse.xtext.common.Terminals 

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.collectionspro.eu/jam/mm/app" as app //application meta-model
import "http://www.collectionspro.eu/jam/mm/app-ops" as ops //operations


ModelRoot returns app::ModelRoot:
	Operations
;

Operations returns app::Operations:
	(operations+=ModelOperation)*
;

ModelOperation returns ops::ModelOperation:
	AddPrimitiveClass
	| AddEmbeddedClass
	| RenameEntity
//diploma
	| AddClass
	| RemoveEntity
	| AddProperty
	| RenameProperty
	| RemoveProperty
	| MoveProperty
	| PullUpProperty
	| PushDownProperty
	| AddParent
	| RemoveParent
	| ExtractClass
	| ExtractSubClass
	| ExtractSuperClass
;

AddPrimitiveClass returns ops::AddPrimitiveClass:
	'addPrimitive' name=ID ':' primitiveType=PrimitiveType
;

AddEmbeddedClass returns ops::AddEmbeddedClass:
	'addEmbedded' name=ID
;

RenameEntity returns ops::RenameEntity:
	'rename' name=ID '->' newName=ID
;

/////////////
// Diploma //
/////////////
AddClass returns ops::AddStandardClass:
	'add' name=ID
;

RemoveEntity returns ops::RemoveEntity:
	'remove' name=ID
;

AddProperty returns ops::AddProperty:
	'add' 
//	(
//		('!ordered' | isOrdered?='ordered')?
//		& ('!unique' | isUnique?='unique')?
//	) 
	owningClassName=ID '.' name=ID ':' typeName=ID 
		('['((upperBound=INT) | (lowerBound=INT '..' (upperBound=INT)?))']')?
		//('=' defaultValue=DefaultValue)?
;

RenameProperty returns ops::RenameProperty:
	'rename' owningClassName=ID '.' name=ID '->' newName=ID
;

RemoveProperty returns ops::RemoveProperty:
	'remove' owningClassName=ID '.' name=ID
;

MoveProperty returns ops::MoveProperties:
	'move' sourceClassName=ID
	(
		'.' propertiesNames+=ID
		| '{' propertiesNames+=ID (',' propertiesNames+=ID)* '}'
	)
	'/' linkName=ID '->' ID
;

PullUpProperty returns ops::PullUpProperties:
	'pullUp' childClassName=ID
	(
		'.' pulledPropertiesNames+=ID
		| '{' pulledPropertiesNames+=ID (',' pulledPropertiesNames+=ID)* '}'
	)
;

PushDownProperty returns ops::PushDownProperties:
	'pushDown' parentClassName=ID
	(
		'.' pushedPropertiesNames+=ID
		| '{' pushedPropertiesNames+=ID (',' pushedPropertiesNames+=ID)* '}'
	)
;

AddParent returns ops::AddParent:
	'set' className=ID '<:' parentClassName=ID
;

RemoveParent returns ops::RemoveParent:
	'set' className=ID '!<:' ID
;

ExtractClass returns ops::ExtractClass:
	'extract' sourceClassName=ID
	(
		'.' propertyNames+=ID
		| '{' propertyNames+=ID (',' propertyNames+=ID)* '}'
	)
	'/' associationPropertyName=ID '->' extractClassName=ID '.' oppositePropertyName=ID
;

ExtractSubClass returns ops::ExtractSubClass:
	'extractSub' sourceClassName=ID
	(
		'.' extractedPropertiesNames+=ID
		| '{' extractedPropertiesNames+=ID (',' extractedPropertiesNames+=ID)* '}'
	)
	'->' extractedClassName=ID
;

ExtractSuperClass returns ops::ExtractSuperClass:
	'extractSuper' 
	(
		sourceClassesName+=ID
		| '{' sourceClassesName+=ID (',' sourceClassesName+=ID)* '}'
	)
	'.'
	(
		propertyNames+=ID
		| '{' propertyNames+=ID (',' propertyNames+=ID)* '}'
	)
	'->' extractParentName=ID
;

/**
 */
enum PrimitiveType returns app::PrimitiveType:
	boolean = 'boolean'
	| char = 'char'
	| int = 'int'
;

enum InheritanceType returns app::InheritanceType:
	joined = 'joined'
	| tablePerClass = 'tablePerClass'
	| singleTable = 'singleTable'
;

Boolean returns ecore::EBoolean:
	'true'
	| 'false'
;

DefaultValue returns ecore::EString:
	ID | INT | Boolean
;

terminal ID:
	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
;
