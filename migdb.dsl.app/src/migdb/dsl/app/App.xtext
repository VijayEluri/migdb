grammar migdb.dsl.app.App with org.eclipse.xtext.common.Terminals 

import "http://www.collectionspro.eu/jam/mm/app" as app //application meta-model
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ModelRoot returns app::ModelRoot:
	Structure
;

Structure returns app::Structure:
	(entities+=ModelEntity)*
;

ModelEntity returns app::ModelEntity:
	GeneralClass
	| PrimitiveClass
;

GeneralClass returns app::GeneralClass:
	EmbeddedClass
	| StandardClass
;

PrimitiveClass returns app::PrimitiveClass:
	'primitive'	name=ID ':' primitiveType=PrimitiveType
;

/**
 * descendants of GeneralClass
 */
EmbeddedClass returns app::EmbeddedClass:
	'embedded'
	name=ID
	'{'
		'id' idProperty=[app::EmbeddedProperty|ID] //FIXME: reference???
		embeddedProperties+=[app::EmbeddedProperty|ID] (embeddedProperties+=[app::EmbeddedProperty|ID])* //FIXME: reference???
    '}'
;

StandardClass returns app::StandardClass:
	(isAbstract?='abstract')? 'class'
	name=ID ('extends' parent=[app::StandardClass|ID] 'by' inheritanceType=InheritanceType)?
	'{'
		('id' idProperty=[app::PrimitiveProperty|ID])? //FIXME: reference???
		(properties+=MultipliableProperty (properties+=MultipliableProperty)*)?
    '}'
;


Property returns app::Property:
	MultipliableProperty
	| EmbeddedProperty
;

MultipliableProperty returns app::MultipliableProperty:
	AssociationProperty
	| PrimitiveProperty
	| NestedProperty
;

EmbeddedProperty returns app::EmbeddedProperty:
	'EmbeddedProperty' isMandatory?='mandatory'	name=ID ':' type=[app::PrimitiveClass|ID]
;


PrimitiveProperty returns app::PrimitiveProperty:
	'PrimitiveProperty'
	(
		isOrdered?='ordered'? &	isUnique?='unique'?
	)
	name=ID
	(
		'['
			upperBound=INT
			| (lowerBound=INT '..'upperBound=INT)
		']'
	)?
	':' type=[app::PrimitiveClass|ID]
	('=' defaultValue=ID)?
;


AssociationProperty returns app::AssociationProperty:
	'AssociationProperty'
	(
		isOrdered?='ordered'? &	isUnique?='unique'?
	)
	name=ID
	(
		'['
			upperBound=INT
			| (lowerBound=INT '..'upperBound=INT)
		']'
	)?
	('oposite' oppositeProperty=[app::AssociationProperty|ID])?
	':' type=[app::StandardClass|ID]
;

NestedProperty returns app::NestedProperty:
	'NestedProperty'
	(
		isOrdered?='ordered'? &	isUnique?='unique'?
	)
	name=ID
		(
		'['
			upperBound=INT
			| (lowerBound=INT '..'upperBound=INT)
		']'
	)?
	':' type=[app::EmbeddedClass|ID]
;

enum PrimitiveType returns app::PrimitiveType:
	boolean = 'boolean'
	| char = 'char'
	| int = 'int'
;

enum InheritanceType returns app::InheritanceType:
	joined = 'joined'
	| tablePerClass = 'tablePerClass'
	| singleTable = 'singleTable'
;

EBoolean returns ecore::EBoolean:
	'true'
	| 'false'
;

terminal ID:
	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
;
