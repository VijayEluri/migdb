module migb.testing.run.test_comparator_component_fail

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.mwe.*


import org.eclipse.xtext.mwe.SlotEntry
import org.eclipse.xtext.mwe.ResourceLoadingSlotEntry
import org.eclipse.xtext.mwe.Validator
import org.eclipse.xtext.mwe.NameBasedFilter

import org.eclipse.emf.mwe.utils.Writer
import org.eclipse.emf.mwe.utils.Reader

import eu.collectionspro.mwe.*
import migdb.generator.*

var TEST_DIR  = "../migdb.qvto/transforms/tests/validator_app/"
var OUTPUT_DIR = "output-tests/test_comparator_component/2-differents/"
/**
 * Test of the ComparatorComponent. Test is unsuccesful - eg, models are different.
 */
Workflow{
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
		directory = "${OUTPUT_DIR}"
	}
	
		
	/* Define of APP metamodel */
	bean=org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri=".."
		registerGeneratedEPackage = "mm.app.AppPackage"
	}
	
		
	/*Create compared file 1 */
	component = QVTOExecutor {
		transformationFile = "${TEST_DIR}/test_app_01.qvto"
		outputSlot = "first"
	}

	/*Create compared file 2 */
	component = QVTOExecutor {
		transformationFile = "${TEST_DIR}/test_app_02.qvto"
		outputSlot = "second"
	}
	
	/* write result */
	component = Writer {
		modelSlot = "first"
		uri = "${OUTPUT_DIR}/expected_result_1.xmi"
		cloneSlotContents = false
		useSingleGlobalResourceSet = true
	}
	
	/* write test */
	component = Writer {
		modelSlot = "second"
		uri = "${OUTPUT_DIR}/test_1.xmi"
		cloneSlotContents = false
		useSingleGlobalResourceSet = true
	}
	
	component = ModelComparator {
		testDescription = "COMPARATOR COMPONENT"
		resultURI = "${OUTPUT_DIR}/expected_result_1.xmi"
		testedURI = "${OUTPUT_DIR}/test_1.xmi"
	}
}