/*
 * The MIT License
 * 
 * Copyright (c) 2010-2011 MigDB team [https://rabbit.felk.cvut.cz/trac/migdb]
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 * 
 */
 
modeltype APP uses app('http://www.collectionspro.eu/jam/mm/app');
modeltype RDB uses rdb('http://www.collectionspro.eu/jam/mm/rdb');

transformation app_to_rdb(in inApp : APP, inout inRdb : RDB);

main() {
	assert(inApp.rootObjects()->size() = 1);
	assert(inApp.rootObjects()->forAll(oclIsKindOf(APP::ModelRoot)));
	
	log("transformation started...");
	
	var appRoot : APP::ModelRoot = inApp.rootObjects()![APP::ModelRoot];
	var rdbRoot : RDB::ModelRoot = inRdb.rootObjects()![RDB::ModelRoot];
	
	appRoot.operations->forEach(op) {
		log("	iterating over operation " + op.repr());
		if (op.isValid(rdbRoot)) then {
			log("	operation is valid " + op.repr());
			op.xmap toRdb(rdbRoot);
		} else {
			log("	operation is invalid " + op.repr());
		} endif;
	};
}

// a mapping of a given operation
abstract mapping APP::operations::ModelOperation::
	toRdb(inout rdbRoot : RDB::ModelRoot);
	
// is a mapping valid
abstract query APP::operations::ModelOperation::
	isValid(inout rdbRoot : RDB::ModelRoot) : Boolean;


query APP::operations::AddClass::isValid(inout rdbRoot : RDB::ModelRoot): Boolean {
	return self.isAbstract = false;
}

mapping APP::operations::AddClass::toRdb(inout rdbRoot : RDB::ModelRoot) {
	// addition of a table to the schema
	rdbRoot.operations += object RDB::operations::AddTable {
		owningSchemaName := "public";
		name := self.name;
	};
	
	// addition of a column to the table
	rdbRoot.operations += object RDB::operations::AddColumn {
		owningSchemaName := "public";
		owningTableName := self.name;
		name := "id";
		type := RDB::PrimitiveType::int;
		// defaultValue := "0";
	};
	
	// addition of an index to the schema
	rdbRoot.operations += object RDB::operations::AddIndex {
		owningSchemaName := "pubic";
		owningTableName := self.name;
		name := "public." + self.name.toLower() + ".id";
		columnsNames := OrderedSet {"id"};
	};
	
	// addition of a primary key to the table
	rdbRoot.operations += object RDB::operations::AddPrimaryKey {
		owningSchemaName := "public";
		owningTableName := self.name;
		name := "public." + self.name.toLower() + ".id.pk";
		underlyingIndexName := "public." + self.name.toLower() + ".id";
	};
}
