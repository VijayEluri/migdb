module migb.run.second

import org.eclipse.emf.mwe.utils.*
import org.eclipse.xtext.mwe.*


import org.eclipse.xtext.mwe.SlotEntry
import org.eclipse.xtext.mwe.ResourceLoadingSlotEntry
import org.eclipse.xtext.mwe.Validator
import org.eclipse.xtext.mwe.NameBasedFilter

import org.eclipse.emf.mwe.utils.Writer
import org.eclipse.emf.mwe.utils.Reader

import eu.collectionspro.mwe.*
import migdb.generator.*

Workflow {
/***** DEFINE META-MODELS *****/
/**/
	bean=org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri=".."
		registerGeneratedEPackage = "mm.app.AppPackage"
	}
/**/
	bean=org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri=".."
		registerGeneratedEPackage = "mm.rdb.RdbPackage"
	}
	
/***** CLEAN DIRECTORIES [SLQ,XMI] *****/	
/**/
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
		directory = "output_sql_second"
	}		
/**/
	component = org.eclipse.emf.mwe.utils.DirectoryCleaner {
		directory = "output_xmi_second"
	}
	
/***** READ MODELS *****/		
/**/			
	component = org.eclipse.emf.mwe.utils.Reader {
		uri="output_xmi_first/outAPP.xmi"
		modelSlot="readModelAPP"
		firstElementOnly=false
	}
/**/	
	component = org.eclipse.emf.mwe.utils.Reader {
		uri="output_xmi_first/outRDB.xmi"
		modelSlot="readModelRDB"
		firstElementOnly=false
	}
	
/***** ADD NEW OPERATIONS TO APP MODEL *****/	
/**/
	component = QVTOExecutor {
		inputSlot = "readModelAPP"
		transformationFile = "../migdb.qvto/transforms/cg/secondAPP.qvto"
		outputSlot = "buildModelAPP"
	}			
		
/***** APP EVOLUTION *****/		
/**/
	component = QVTOExecutor {
		inputSlot = "buildModelAPP"
		transformationFile = "../migdb.qvto/transforms/evolution/populate_generations_app.qvto"
		outputSlot = "generationModelAPP"
	}
	
/***** ORM MAPPING *****/	
/**/
	component = QVTOExecutor {
		inputSlot = "generationModelAPP"
		inOutSlot = "readModelRDB"
		transformationFile = "../migdb.qvto/transforms/orm/orm_operations.qvto"
	}
	
/***** RDB EVOLUTION *****/	
/**/
	component = QVTOExecutor {
		inputSlot = "readModelRDB"
		transformationFile = "../migdb.qvto/transforms/evolution/populate_generations_rdb.qvto"
		outputSlot = "generationModelRDB"
	}
	
/***** WRITE XMI FILES *****/	
/**/
	component = Writer {
		modelSlot = "generationModelAPP"
		uri = "output_xmi_second/outAPP.xmi"
		cloneSlotContents = false
		useSingleGlobalResourceSet = true
	}
/**/
	component = Writer {
		modelSlot = "generationModelRDB"
		uri = "output_xmi_second/outRDB.xmi"
		cloneSlotContents = false
		useSingleGlobalResourceSet = true
	}
	
/***** SQL GENERATOR *****/	
/**/
	component = CodeGenComponent {
		withoutModel = false
		slot = "generationModelRDB"
		generator = Generator {}
		outputPath = "output_sql_second/"
	}
	
/***** DATABASE CONNECT *****/	
/**/
	component = CommandLineExec{
		command = "ruby ../migdb.ruby.sql/script.rb output_sql_second postgres postgres 123456"
	}		
/**/
}